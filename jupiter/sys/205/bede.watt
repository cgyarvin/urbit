^-  bowl
=>  |%
    ++  game                                      ::  session state
      $:  nub=vase                                ::  system + libraries
          red=vase                                ::  result log
          ser=vase                                ::  source log
      ==
    ++  sess  ,@                                  ::  session identity
    --
=+  ^=  pit                                       ::  application state
    :*  sys=*(map sess game)
    ==
=>  |%
    ++  numb
      |=  san=span  ^-  ?
      =+  yes=(slay san)
      ?=([~ %ud *] yes)
    --
|%
++  boot
  |=  [now=date loc=path]
  ^-  [p=deed q=bowl]
  :-  :-  %all
      :~  :-  %hunt 
          :^    %%
              [[~ 0] [%y %pin numb ~]]
            0
          |=  [seq=@ hap=path deb=bill]
          ^-  lime
          ?.  ?=(@ q.deb)
            ~
          =+  yes=(slay (snag 2 hap))
          ?>  ?=([~ %% %ud *] yes)
          [%run %line p.u.yes q.deb]
      ::
          [%say %yo %p 0 0(show 'hello, bede.')]
      ==
  ^?(..boot)
:: 
++  peek
  |=  [now=date cam=lens rel=path]
  [~ ~]
::
++  poke
  |=  [hov=hive man=buck]
  ~&  (show 'bede: poke')
  ^-  [p=lime q=bowl]
  ?+    man  !!
      [%line p=sess q=disc]
    =+  ^=  fub  ^-  [p=term q=gene]
        %+  rash
          q.man
        ;~  pose 
          (stag %% wide:vast)
          ;~  plug
            ;~(pfix col sym)
            ;~(pfix ace wide:vast)
          ==
        ==
    =+  xac=(~(get by sys.pit) p.man)
    =+  ^=  tuy  ^-  game
        ?:  ?=(^ xac)
          u.xac
        [seed [[%atom %n] ~] [[%atom %n] ~]]
    =+  dof=(slop red.tuy ser.tuy)
    =+  ^=  vax  ^-  vase
        (slop [[%face %% p.dof] q.dof] nub.tuy)
    =+  wol=(slap vax q.fub)
    ?+    p.fub  !!
        %%
      =+  ham=~(dole ut p.wol)
      :-  [%say %yo %p 0 0 (dish:ut ham q.wol)]
      %=  ..poke
        sys.pit  %+  ~(put by sys.pit) 
                   p.man 
                 %=  tuy
                   red  (slop wol red.tuy)
                   ser  (slop [[%atom %ta] ~] ser.tuy)
                 ==
      ==
    ==
  ==
--
