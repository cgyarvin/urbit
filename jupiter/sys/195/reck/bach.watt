!:
::          %bach, a shell.   This file is in the public domain.
::
:-  %bach
^-  dock
|=  [now=time wha=@ta]
%^    hull
    (iris now)
  wha
^-  bead
|=  [now=time wha=@ta]
:-  ^-  lime
    :-  %all
    :~  :^    %rod
            %abel
          :-  [~ ~]
          :-  [%eyre %pine ~]
          |=  hap=*  ^-  (unit)
          ?.  ?=([p=@ta ~] hap)
            ~
          [~ (need (clan %p p.hap))]
        |=([seq=@ud muz=* cax=bill] [%run muz q.cax])
    ==
^-  vane
=>  ..$
=>  |%
    ++  game                                            ::  system state 
      $:  maw=(map plot _*berg)
      ==
    ++  move  ,[p=plot q=boat r=card]                   ::  external action
    --
=+  sys=*game
|%
++  peek  |=([cam=lens hap=path] ~)
++  poke  
  |=  [cam=lens man=*] 
  ^-  [p=lime q=vane]
  =+  muv=((hard move) man)
  =+  yem=~(rent co [~ %p p.muv])
  =+  ^=  beg  ^+  *berg
      =+  beg=(~(get by maw.sys) p.muv)
      ?^  beg  u.beg
      %+  berg
        p.muv
      |=(nam=* (peek:cam [%cary (path nam)]))
  =+  yub=(knap:beg when:cam r.muv)
  :-  :-  %all
      %+  turn  p.yub
      |=  gif=gift
      `lime`[%say %pi [%pout yem ~] [[~ q.muv] gif]]
  +>.$(maw.sys (~(put by maw.sys) p.muv q.yub))
--
