=>  |%
    ++  bole  ,[p=book q=(map span bole)]
    ++  book  (map wine life)
    ++  dirt  ,[p=file q=(map span dirt)]
    ++  file  (map wine ,[p=time q=note])
    ++  life  (list ,[p=time q=(unit note)])
    ++  note  ,[l=@ d=@]
    ++  wine  term
    --
|%
++  on
  |_  lob=bole
  ++  dear
    |=  [now=time ban=barn]
    ~|  %on-dear
    ^-  dirt
    ?^  ban
      =+  hyn=(~(get by q.lob) i.ban)
      ?~  hyn
        [~ ~]
      $(ban t.ban, lob u.hyn)   
    =+  ^=  nic
      |=  saq=life  ^-  [p=time q=(unit note)]
      ?~(saq [0 ~] ?:((gte now p.i.saq) i.saq $(saq t.saq)))
    =+  ^=  may
      |=  pug=book
      %-  ~(gas by *(map wine ,[p=time q=note]))
      =+  tox=(~(tap by pug) *(list ,[p=wine q=life]))
      |-  ^-  (list ,[p=wine q=[p=time q=note]])
      ?~  tox
        ~
      =+  mor=$(tox t.tox)
      =+  naq=(nic q.i.tox)
      ?~(q.naq mor [[p.i.tox [p.naq u.q.naq]] mor])
    :-  (may p.lob)
    %-  ~(gas by *(map span dirt))
    =+  tig=(~(tap by q.lob) *(list ,[p=span q=bole]))
    |-  ^-  (list ,[p=span q=dirt])
    ?~  tig
      ~
    =+  mor=$(tig t.tig)
    =+  naq=^$(lob q.i.tig)
    ?:(=([~ ~] naq) mor [[p.i.tig naq] mor])
  ::
  ++  hunt
    |=  [now=time wyn=wine ban=barn]  
    ~|  %on-hunt
    ^-  (unit note)
    ?^  ban
      =+  hyn=(~(get by q.lob) i.ban)
      ?~(hyn ~ $(ban t.ban, lob u.hyn))
    =+  yow=(~(get by p.lob) wyn)
    ?~  yow
      ~
    |-  ^-  (unit note)
    ?~  u.yow
      ~
    ?:  (gte now p.i.u.yow)
      q.i.u.yow
    $(u.yow t.u.yow)
  ::
  ++  save
    |=  [now=time wyn=wine ban=barn fal=(unit note)]
    ~|  %on-save
    ^-  bole
    ?~  ban
      :-  %+  ~(put by p.lob)
            wyn
          :-  [now fal]
          =+  yow=(~(get by p.lob) wyn)
          ?:  |(?=(~ yow) ?=(~ u.yow))
            ~
          ?>((gth now p.i.u.yow) u.yow)
      q.lob 
    :-  p.lob
    %+  ~(put by q.lob)
      i.ban
    %=    $
        ban  t.ban
        lob
      =+  hyn=(~(get by q.lob) i.ban)
      ?~  hyn
        [~ ~]
      u.hyn
    ==
  --
--


