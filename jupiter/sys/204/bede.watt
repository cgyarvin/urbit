^-  crib
=>  |%
    ++  game                                      ::  session state
      $:  nub=vase                                ::  system + libraries
          red=vase                                ::  result log
          ser=vase                                ::  source log
      ==
    ++  sess  ,@                                  ::  session identity
    --
=+  ^=  pit                                       ::  application state
    :*  sys=*(map sess game)
    ==
=>  |%
    ++  numb
      |=  san=span  ^-  ?
      =+  yes=(slay san)
      ?=([~ %ud *] yes)
    --
|%
++  boot
  |=  [now=date loc=path]
  ^-  [p=deed q=crib]
  :-  :-  %more
      :~  :-  %hunt 
          :^    %%
              [[~ 0] [%y %pin numb ~]]
            0
          |=  [seq=@ hap=path deb=bill]
          ^-  deed
          ?.  ?=(@ q.deb)
            ~
          =+  yes=(slay (snag 2 hap))
          ?>  ?=([~ %% %ud *] yes)
          [%feed %line p.u.yes q.deb]
      ::
          [%give %yo 0 (show 'hello, bede.')]
      ==
  ^?(..boot)
:: 
++  peek
  |=  [now=date cam=lens rel=path]
  [~ ~]
::
++  poke
  |=  [now=date cam=lens man=buck]
  ~&  (show 'bede: poke')
  ^-  [p=deed q=crib]
  ?+    man  !!
      [%line p=sess q=disc]
    =+  ^=  fub  ^-  [p=term q=gene]
        %+  rash
          q.man
        ;~  pose 
          (stag %% wide:vast)
          ;~  plug
            ;~(pfix col sym)
            ;~(pfix ace wide:vast)
          ==
        ==
    =+  xac=(~(get by sys.pit) p.man)
    =+  ^=  tuy  ^-  game
        ?:  ?=(^ xac)
          u.xac
        [seed [[%atom %n] ~] [[%atom %n] ~]]
    =+  dof=(slop red.tuy ser.tuy)
    =+  ^=  vax  ^-  vase
        (slop [[%face %% p.dof] q.dof] nub.tuy)
    =+  wol=(slap vax q.fub)
    ?+    p.fub  !!
        %%
      =+  ham=~(dole ut p.wol)
      :-  [%give [%yo 0 (dish:ut ham q.wol)]]
      %=  ..poke
        sys.pit  %+  ~(put by sys.pit) 
                   p.man 
                 %=  tuy
                   red  (slop wol red.tuy)
                   ser  (slop [[%atom %ta] ~] ser.tuy)
                 ==
      ==
    ==
  ==
--
