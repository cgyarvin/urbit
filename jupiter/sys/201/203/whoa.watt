  ::
  ++  weed
    |=  a=@t
    ^-  @ta
    %+  rap  3
    |-  ^-  (list ,@)
    ?:  =(0 a)  
      ~
    =+  b=(teff a)
    =+  c=(turf (end 3 b a))
    =+  d=$(a (rsh 3 b a))
    ?:  ?|  &((gte c 'a') (lte c 'z')) 
            &((gte c 'A') (lte c 'Z'))
            &((gte c '0') (lte c '9'))
            =('-' c)
        ==
      [c d]
    ?+  c  :-  '~' 
           |-  ^-  tape
           ?:  =(0 c)
             ['.' d]
           =+  e=(dec (met 2 c))
           =+  f=(rsh 2 e c)
           [(add ?:((lte f 9) 48 87) f) $(c (end 2 e a))]
      ' '  ['.' d]
      '.'  ['~' '.' d]
      '~'  ['~' '~' d]
    ==
  ::
  ++  woad
    |=  a=@ta
    ^-  @t
    %+  rap  3
    |-  ^-  (list ,@)
    ?:  =(0 a)
      ~
    =+  b=(end 3 1 a)
    =+  c=(rsh 3 1 a)
    ?:  =('.' b)
      [' ' $(a c)]  
    ?.  =('~' b)
      [b $(a c)]  
    =>  .(b (end 3 1 c), c (rsh 3 1 c))
    ?+  b  =-  (weld (rip 3 (turf p.d)) $(a q.d))
           ^=  d
           =+  d=0
           |-  ^-  [p=@ q=@]
           ?<  =(0 c)
           =+  [e=(end 3 1 c) f=(rsh 3 1 c)]
           ?:  =('.' e)
             [d f]
           %=    $
              c  f
              d  %+  add
                   (mul 16 d)
                 %+  sub       
                   e
                 ?:  &((gte e '0') (lte e '9'))
                   48
                 ?>(&((gte e 'a') (lte e 'z')) 87)
           ==
      '.'  ['.' $(a c)]
      '~'  ['~' $(a c)]
    == 


