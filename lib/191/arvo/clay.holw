!:
::  clay (4c), revision control
::
|=  pit=vase
^-  vane
=>
  ::::::::::::::::::::::::::::::::::::::::::::::::::::::::
  ::              section 4cA, filesystem logic         ::
  ::
  |%
  ::
  ++  un                                                ::  per seat
    |=  [who=@p now=@da ruf=raft]
    =+  ^=  yar  ^-  room
        =+  yar=(~(get by fat.ruf) who)
        ?~(yar *room u.yar)
    =|  byn=(list ,[p=duct q=riot]) 
    =|  say=(list ,[p=duct q=seat r=[p=@ud q=riff]])
    |%
    ++  de                                              ::  per desk
      |_  [buk=@ta saq=desk]
      ++  aeon                                          ::  mark to era
        |=  lok=mark
        ^-  (unit ,@ud)
        ?-    -.lok
            %da
          ?:  (gth p.lok now)  ~
          =+  deq=?:(=(0 let.saq) ~ [~ (dec let.saq)])
          |-  ^+  deq
          ?~  hit.saq  deq
          ?:  (gth p.lok p.i.hit.saq)  deq
          =+  nix=(dec let.saq)
          $(hit.saq t.hit.saq, let.saq nix, deq [~ nix])
        :: 
            %tas  (~(get by lab.saq) p.lok)
            %ud   ?:((gte p.lok let.saq) ~ [~ p.lok])
        ==
      ::
      ++  amor                                          ::  endpoint query
        |=  ren=?(%x %y %z)
        ^-  (unit ,*)
        ?-  ren
          %x  ?.(?=(& -.arc.saq) ~ [~ q.arc.saq])
          %y  :-  ~
              ?-  -.arc.saq
                &  [& p.arc.saq]
                |  [| (turn (~(tap by p.arc.saq) ~) |=([p=@ta *] p))]
              ==
          %z  [~ arc.saq]
        ==
      ::
      ++  ammo                                          ::  trace window
        |=  oan=@ud
        =|  bul=(list ,[p=@da q=ukay])
        |-  ^+  [bul +>.^$]
        ?:  =(oan (dec let.saq))  [(flop bul) +>.^$]
        %=  $
          bul      [[p.i.hit.saq r.i.hit.saq] bul]
          let.saq  (dec let.saq)
          hit.saq  t.hit.saq
          arc.saq  dark:(durn:ka:(ci arc.saq) q.i.hit.saq)
        ==
      ::
      ++  argo  |=(oan=@ud +:(ammo oan))                ::  revert to era
      ++  aver                                          ::  domestic read 
        |=  [ren=mare lok=mark way=path]
        ^-  (unit (unit ,*))
        =+  nao=(aeon lok)
        ::  ~&  [%aver buk lok nao]
        ?~  nao  ~
        :-  ~
        ?:  ?=(%w ren)
          ?.(?=(~ way) ~ [~ u.nao])
        =.  +>.$  (argo u.nao)
        (amor(arc.saq dark:(deny:ka:(ci arc.saq) way)) ren)
      ::
      ++  biff                                          ::  path subset
        |=  [yok=path gib=path]
        ^-  (unit path)
        ?~  yok  [~ gib]
        ?~  gib  ~
        ?.  =(i.yok i.gib)  ~
        $(yok t.yok, gib t.gib)
      ::
      ++  blab                                          ::  ship result
        |=  [hen=duct ren=mare lok=mark way=path dat=*]
        ^+  +>
        +>(byn [[hen ~ [ren lok buk] way dat] byn])
      ::
      ++  demo                                          ::  ship snapshot
        |=  [hen=duct ren=mare lok=mark way=path]
        ^+  +>
        %=    +>
            byn
          =<  byn
          ^+  +>
          ?:  ?=(%w ren)
            ?^  way  +>
            (blab hen ren lok ~ let.saq)
          =.  arc.saq  dark:(deny:ka:(ci arc.saq) way)
          ?:  ?=(%z ren)
            (blab hen ren lok way arc.saq)
          =+  yaw=(flop way)
          |-  ^+  +>.^$
          ?-    -.arc.saq
              &  
            (blab hen ren lok (flop yaw) ?:(?=(%y ren) [& p.arc.saq] q.arc.saq))
          ::
              |
            =+  goz=(~(tap by p.arc.saq) ~)
            =>  .(arc.saq `arch`arc.saq)
            =.  +>.^$  
              ?.  ?=(%y ren)  +>.^$
              (blab hen ren lok (flop yaw) [| (turn goz |=([p=@ta *] p))])
            |-  ^+  +>.^^$
            ?~  goz  +>.^^$
            $(+>.^^$ ^$(yaw [p.i.goz yaw], arc.saq q.i.goz))
          ==
        ==
      ::
      ++  doze
        =+  xiq=(~(tap by qyx.saq) ~)                   ::  XX tap
        =|  nex=(unit ,@da)
        |-  ^+  nex
        ?~  xiq  nex
        ?.  ?=(%da -.q.q.i.xiq)  $(xiq t.xiq)
        %=    $
            xiq  t.xiq
            nex
          %+  hunt  nex
          ?:  (lth now p.q.q.i.xiq)  [~ p.q.q.i.xiq]
          ?~(q.q.q.i.xiq ~ ?>((lth now u.q.q.q.i.xiq) q.q.q.i.xiq))
        ==
      ::
      ++  ease                                          ::  unsubscrube
        |=  hen=duct
        ^+  +>
        +>(qyx.saq (~(del by qyx.saq) hen))
      ::
      ++  eave                                          ::  subscribe
        |=  [hen=duct ren=mare myn=morn way=path]
        ^+  +>
        ?-    -.myn
            %da
          =+  nao=(aeon [%da p.myn])
          ?~  nao
            +>.$(qyx.saq (~(put by qyx.saq) hen ren myn way))
          =+  oap=?~(q.myn ~ (aeon %da u.q.myn))
          =+  ^=  nyb  ^+  byn
              =.  +>.$  ?~(oap +>.$ (argo u.oap))
              =^  giz  +>.$  (ammo u.nao)
              =.  +>.$  (demo hen ren [%da p.myn] way)
              =<  byn
              |-  ^+  +>.^$
              ?~  giz  +>.^$
              =.  +>.^$  (ecce q.i.giz hen ren [%da p.i.giz] way)
              $(+>.^$ (edit(now p.i.giz) q.i.giz))
          %=  +>.$
            byn      ?~(oap nyb [[hen ~] nyb])
            qyx.saq  ?^(oap qyx.saq (~(put by qyx.saq) hen ren myn way))
          ==
        ::
            ?(%tas %ud)
          =+  lok=`mark`?-(-.myn %tas [%tas p.myn], %ud [%ud p.myn])
          =+  nao=(aeon lok)
          ?~  nao  
            +>.$(qyx.saq (~(put by qyx.saq) hen ren myn way))
          +>.$(byn [[hen ~] byn:(demo:(argo u.nao) hen ren lok way)])
        ==
      ::
      ++  ecce                                          ::  send changes
        |=  [lav=ukay hen=duct ren=mare lok=mark way=path]
        ^+  +>
        ?-    ren
            %w
          ?^  way  +>
          (blab hen ren lok ~ (dec let.saq))
        ::
            %x
          |-  ^+  +>.^$
          ?~  lav  +>.^$
          ?.  ?=(%set -.q.i.lav)  $(lav t.lav)
          =+  gim=(biff way p.i.lav)
          ?~  gim  $(lav t.lav)
          $(lav t.lav, +>.^$ (blab hen ren lok p.i.lav r.q.i.lav))
        ::
            %y
          !!
        ::
            %z
          !!
        ==
      ::
      ++  ecco                                          ::  ship release
        |=  lol=@tas
        ^+  +>
        ?<  |(=(0 let.saq) (~(has by lab.saq) lol))
        =.  lab.saq  (~(put by lab.saq) lol (dec let.saq))
        =+  xiq=(~(tap by qyx.saq) ~)                   ::  XX tap
        |-  ^+  +>.^$
        ?~  xiq  +>.^$
        ?.  &(?=(%tas -.q.q.i.xiq) =(lol p.q.q.i.xiq)) $(xiq t.xiq)
        $(xiq t.xiq, +>.^$ (demo p.i.xiq p.q.i.xiq [%tas lol] r.q.i.xiq))
      ::
      ++  edit                                          ::  apply changes
        |=  lav=ukay
        ^+  +>
        ::  ~&  [%edit buk lav]
        =+  wak=darn:(durn:ka:(ci arc.saq) lav)
        %=  +>.$
          arc.saq  q.wak
          let.saq  +(let.saq)
          hit.saq  :_(hit.saq [now p.wak lav])
        ==
      ::
      ++  exec                                          ::  propagate changes
        |=  lav=ukay
        ^+  +>
        =+  xiq=(~(tap by qyx.saq) ~)                   ::  XX tap
        =|  dem=(list ,[p=duct q=mare r=mark s=path])
        |-  ^+  +>.^$
        ?~  xiq  
          =.  +>.^$  (edit lav)
          |-  ^+  +>.^^$
          ?~  dem  +>.^^$
          =.  +>.^^$  (demo i.dem)
          =.  byn  [[p.i.dem ~] byn]
          $(dem t.dem)
        ?-    -.q.q.i.xiq
            %da
          %=  $
            xiq    t.xiq
            +>.^$  (ecce lav p.i.xiq p.q.i.xiq [%da now] r.q.i.xiq)
          ==
            %tas  $(xiq t.xiq)
            %ud
          ?.  =(let.saq p.q.q.i.xiq)  $(xiq t.xiq)
          %=  $
            xiq      t.xiq
            dem      :_(dem [p.i.xiq p.q.i.xiq [%ud let.saq] r.q.i.xiq])
            qyx.saq  (~(del by qyx.saq) p.i.xiq)
          ==
        ==
      ::
      ++  wake                                          ::  react to time
        ^+  .
        =+  xiq=(~(tap by qyx.saq) ~)                   ::  XX tap
        |-  ^+  ..wake
        ?~  xiq  ..wake
        ?.  ?=(%da -.q.q.i.xiq)  $(xiq t.xiq)
        =.  ..wake
          ?.  &((lth las.ruf p.q.q.i.xiq) (gte now p.q.q.i.xiq))  ..wake
          (demo p.i.xiq p.q.i.xiq [%da now] r.q.i.xiq)
        =.  byn
          ?.  &(?=(^ q.q.q.i.xiq) (gte now u.q.q.q.i.xiq))  byn
          [[p.i.xiq ~] byn]
        $(xiq t.xiq)
      ::
      ++  zoot
        %_(..de dos.yar (~(put by dos.yar) buk saq))
      --
    ::
    ++  di
      |=  buk=@ta
      =+  ^=  saq  ^-  desk
          =+  saq=(~(get by dos.yar) buk)
          ?~(saq [[%| ~] 0 ~ ~ ~] u.saq)
      ~(. de buk saq)
    ::
    ++  fa
      |=  him=seat
      =+  ^=  raz
          =+  raz=(~(get by rid.yar) him)
          ?~(raz *rind u.raz)
      |%
      ++  mote                                          ::  send/cancel request
        |=  [hen=duct ryf=riff]
        ^+  +>
        =+  nux=(~(get by fod.raz) hen)
        ?^  nux
          ?>  ?=(~ q.ryf)
          %=  +>.$
            say      [[[[%c (scot %ud u.nux) ~] hen] him [u.nux ryf]] say]
            fod.raz  (~(del by fod.raz) hen)
            bim.raz  (~(del by bim.raz) u.nux)
          ==
        ?>  ?=(^ q.ryf)
        =+  inx=nix.raz
        %=  +>.$
          say      [[[[%c (scot %ud inx) ~] hen] him [inx ryf]] say]
          nix.raz  +(nix.raz)
          bim.raz  (~(put by bim.raz) inx [hen ryf])
          fod.raz  (~(put by fod.raz) hen inx)
        ==
      ::       
      ++  tome                                          ::  accept response
        |=  [inx=@ud rot=riot]
        ^+  +>
        ::  ~&  [%clay-tome inx rot]
        =+  mub=(~(get by bim.raz) inx)
        ?~  mub  +>.$
        %=  +>.$
          byn      [[p.u.mub rot] byn]
          bim.raz  (~(del by bim.raz) inx)
          fod.raz  (~(del by fod.raz) p.u.mub)
          hac.raz  ?~  rot 
                     hac.raz 
                   %+  ~(put by hac.raz) 
                     [p.p.u.rot q.p.u.rot r.p.u.rot q.u.rot]
                   r.u.rot
        ==
      ::
      ++  tref                                          ::  request rejected
        |=  tea=wire
        ^+  +>
        ?:  =(%good cap)  +>
        ?.  ?=([@ ~] tea)  +>
        =+  xoc=(slay i.tea)
        ?.  ?=([~ %% %ud @] xoc)  +>.$
        =+  mub=(~(get by bim.raz) q.p.u.xoc)
        ?~  mub  +>.$
        %=  +>.$
          byn      [[p.u.mub ~] byn]
          fod.raz  (~(del by fod.raz) q.p.u.xoc)
        ==
      ::
      ++  vera                                          ::  search
        |=  [buk=@tas ren=mare lok=mark way=path]
        (~(get by hac.raz) ren lok buk way)
      ::
      ++  zoom
        %_(+>.$ rid.yar (~(put by rid.yar) him raz))
      --
    ::
    ++  doze
      =+  saz=(turn (~(tap by dos.yar) ~) |=([a=@tas b=*] a))
      =|  nex=(unit ,@da)
      |-  ^+   nex
      ?~  saz  nex
      $(saz t.saz, nex (hunt nex doze:(di i.saz)))
    ::
    ++  wake
      ^+  .
      =+  saz=(turn (~(tap by dos.yar) ~) |=([a=@tas b=*] a))
      |-  ^+  ..wake
      ?~  saz  ..wake
      $(saz t.saz, ..wake zoot:wake:(di i.saz))
    ::
    ++  zonk
      ^-  [(list move) raft]
      :-  %+  weld
            %+  turn  (flop byn) 
            |=([a=duct b=riot] [[~ who] a [%writ b]])
          %+  turn  (flop say) 
          |=([a=duct b=seat c=*] [[~ who] [/a/ a] [%want b %re c]])
      ruf(fat (~(put by fat.ruf) who yar))
    --
  --
  ::::::::::::::::::::::::::::::::::::::::::::::::::::::::
  ::              section 4cA, filesystem vane          ::
  ::
  =|                                                    ::  instrument state
      $:  ruf=raft                                      ::  revision tree
      ==                                                ::
  |=  [now=@da eny=@ sky=||(* (unit))]                  ::  activate
  ^?                                                    ::  opaque core
  |%                                                    ::
  ++  beat                                              ::  update
    |=  [whu=(unit seat) tea=wire hen=duct fav=card]
    ^-  [p=(list move) q=vane]
    ?+    -.fav  ~&  [%clay-none fav]
                 !!
        %crud
      [[[whu [/d/ hen] %flog fav] ~] ..^$]
    ::
        %edit
      =.  whu  
          ?^  whu  whu
          ?.  =(%gold (adit hen))  ~
          [~ p.fav] 
      ?>  =(u.whu p.fav)
      =+  ^=  yaq  ^-  (list move)
          =+  one=~(rend co ~ %p u.whu)
          =+  two=(rip 3 q.fav)
          %+  turn
            r.fav
          |=  uke=ukaz  ^-  move
          =+  qua=~(ram re (dish:ut [~ %path] p.uke))
          =+  qui=?-(-.q.uke %del '-', %set ?:(=(0 q.q.uke) '+' ':'))
          [whu [/d/ hen] %flog %note qui %leaf "{one}/{two}{qua}"]
      =^  mos  ruf  zonk:zoot:(exec:(di:wake:(un u.whu now ruf) q.fav) r.fav)
      [(weld yaq mos) ..^$]
    ::
        %keep
      ::  [[%tell %0 %leaf "clay: home for {~(rend co ~ %p u.whu)}"] ~]
      [~ ..^$(fat.ruf (~(put by fat.ruf) u.whu *room))]
    ::
        %ship
      ?>  ?=(^ whu)
      =^  mos  ruf  zonk:zoot:(ecco:(di:wake:(un u.whu now ruf) p.fav) q.fav)
      [mos ..^$]
    ::
        %warp
      ?>  ?=(^ whu)
      =^  mos  ruf
        =<  zonk
        =+  une=(un u.whu now ruf)
        ::  ~&  [%warp u.whu p.fav]
        ?.  =(u.whu p.fav)
          zoom:(mote:(fa:une p.fav) hen q.fav)
        =+  dic=(di:une p.q.fav)
        ?~  q.q.fav
          zoot:(ease:dic hen)
        zoot:(eave:dic hen u.q.q.fav)
      [mos ..^$]
    ::
        %wart
      ?>  ?=(%ru q.fav)
      ?~  s.fav  [~ ..^$]
      =^  mos  ruf
        =<  zonk
        =<  zoom
        (tome:(fa:(un u.whu now ruf) p.fav) ((hard ,[@ud riot]) u.s.fav))
      [mos ..^$]
    ::
        %went
      ?:  =(%good q.fav)  [~ ..^$]
      =^  mos  ruf
        zonk:zoom:(tref:(fa:(un u.whu now ruf) p.fav) tea)
      [mos ..^$]
    ::
        %wake
      =+  dal=(turn (~(tap by fat.ruf) ~) |=([a=@p b=room] a))
      =|  mos=(list move)
      |-  ^-  [p=(list move) q=vane]
      ?~  dal  [mos ..^^$(las.ruf now)] 
      =^  som  ruf  zonk:wake:(un i.dal now ruf)
      $(dal t.dal, mos (weld som mos))
    ==
  ::
  ++  come  |=(old=vase (load old))
  ++  doze
    |=  [now=@da hen=duct]
    =|  nex=(unit ,@da)
    =+  dal=(turn (~(tap by fat.ruf) ~) |=([a=@p b=room] a))
    |-  ^+  nex
    ?~  dal  nex
    $(dal t.dal, nex (hunt nex doze:(un i.dal now ruf)))
  ::
  ++  flee  stay
  ::
  ++  load
    |=  new=vase
    ^-  vane
    ?.  (~(nest ut -:!>(ruf)) & p.new)
      ~|(%load-nest-clay !!)
    ..^$(ruf (raft q.new))
  ::
  ++  raze
    ^-  vane
    ..$(ruf *raft)
  ::
  ++  scry                                              ::  inspect
    |=  [our=seat ron=@tas his=seat lot=coin tyl=path]
    ^-  (unit)
    ?~  tyl  ~
    =+  poj=(slay i.tyl)
    ?.  ?=([~ %% %tas *] poj)  ~
    =+  luk=?.(?=(%% -.lot) ~ ((soft mark) p.lot))
    =+  run=((soft mare) ron)
    ?~  luk  ~
    ?~  run  ~
    ?.  =(our his)
      (vera:(fa:(un our now ruf) his) q.p.u.poj u.run u.luk t.tyl)
    =+  vyr=(aver:(di:(un our now ruf) q.p.u.poj) u.run u.luk t.tyl)
    ?~(vyr ~ u.vyr)
  ::
  ++  stay  `vase`!>(ruf)
  --
