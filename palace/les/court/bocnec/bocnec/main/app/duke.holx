!:
::        /main/app/duke/: issue a class C flag, from a class B flag.
::
=>  !#(/===$main/lib/hume/)
|=  [who=flag est=time eny=@uw was=path]
|=  *
|=  ~
^-  bowl
%+  pomp  ""
%+  pomp  "            If I did not build for myself"
%+  pomp  "            for whom should I build?"
%+  pomp  ""
%+  pomp  "                  -- Bunting, _Chomei at Toyama_"
%+  pomp  ""
?.  =(2 (met 3 who))
  [[[%text "{<who>}: not entitled to issue dukes"] ~] ~]
|-  ^-  bowl
%+  (polo %text "a ticket for the duke: " ~)
  %+  sear  
    |=  a=dime 
    ?.  &(=(2 (met 4 q.a)) =(who (end 4 1 q.a)))  ~  
    [~ u=`@p`q.a]
  ;~(pose bisk:so (stag %p ;~(pfix sig fed:ag)))
|=  duc=flag
^-  bowl
%+  (polo %pass "entropy: " ~)
  (boss 256 (more gon qit))
|=  tey=@
=.  tey  (shax tey)
%+  pomp  "entropy check: {<`@p`(mug tey)>}"
%+  (polo %text "first name: " ~)
  (boss 256 (more gon qit))
|=  fin=@
%+  (polo %text "last name: " ~)
  (boss 256 (more gon qit))
|=  sur=@
%+  (polo %text "year of birth: " ~)
  dim:ag
|=  yar=@
=+  wum=`whom`[yar %us-ca [fin ~ ~ sur]]
=+  buc=(buck .^(%a '0' (scot %p who) %buck ~))
?>  ?=(^ p.buc)
=+  rud=(wear q.i.p.buc)
=+  bur=(shax :(mix duc (sham buc) tey))
=+  loy=(brew 2.048 bur)
=+  mac=`mace`[[0 sec:ex:loy] ~]
=+  syp=`step`[`bran`[0 [~ p.i.p.buc] duc est] [%duke %lord wum] pub:ex:loy]
=+  ded=`deed`[(sign:se:rud *code (shaf %meld (sham syp))) syp]
=+  wil=`will`[ded q.buc]
=+  nuv=(rsh 3 1 ~(rent co %% %p duc))
:-  :~  [%text "saved ticket for {<duc>} (#{<`@ud`duc>})."]
        [%text "whom: {<wum>}"]
        [%text "fingerprint: {<`@uw`fig:ex:loy>}"]
        [%save [nuv %atom ~] (jam duc mac wil)]
    ==
~
