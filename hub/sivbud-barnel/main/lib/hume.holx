!:
::  application standard library
::
=>  
  |%                                                      ::  models
  ++  cron  ?(%day %hour %minute %none %second)           ::  wake frequency
  ++  gift                                                ::  web event 
            $%  [%cron p=cron]                            ::  wakeup
                [%form p=pact q=quay]                     ::  posted form
                [%note p=path q=note]                     ::  extrinsic note
                [%post p=pact q=mime q=octs]              ::  non-form post
                [%putt p=pact q=(list lump)]              ::  put
                [%user p=user]                            ::  user event
            ==                                            ::
  ++  lamp                                                ::  simple web app
            $_  ^?  |%                                    ::  opaque object
            ++  give                                      ::  serve
              |=  [now=@da fig=gift]                      ::  time, request
              :-  p=*(list card)                          ::  act
              q=*(unit ,[p=(list slip) q=lamp])           ::  request/continue
            ::                                            ::
            ++  miss                                      ::  redirect?
              |=  [pac=pact ced=cred]                     ::  test and apply
              ^-  (unit purl)                             ::
              ~                                           ::
            --                                            ::
  ++  lump  ,[p=path q=mime r=octs]                       ::  submitted data
  ++  wick  vase                                          ::  vase of lamp
  ++  user  ?(%born %came %died %left)                    ::  user event
  --                                                      ::
|%                                                        ::  functions
++  lunt                                                  ::  web framework
  |=  :*  who=flag                                        ::  owner
          ped=cron                                        ::  wake frequency
          rut=(list rout)                                 ::  routes to
          ras=wick                                        ::  server state
      == 
  ^-  bowl
  =+  ^=  hup  ^-  (list slip)
      :~  [// [%ht rut]]
      ==
  :-  *(list card)
  :-  ~
  :-  hup 
  |=  [now=@da pax=path nut=note]
  ^-  bowl
  =+  [saw=*(list card) ask=*(list slip)]
  =<  zing:wist
  |%
  ++  send                                                ::  dispatch event
    |=  bax=gift
    ^+  +>
    =+  sam=!>(now bax)
    =+  gat=(slap ras [%cnbc %give])
    =+  pro=(slam gat sam)
    =+  [wax=(slot 2 pro) hin=(slot 3 pro)]
    =.  saw  (weld ((hard (list card)) q.wax) saw)
    ?:  =(~ q.hin)
      +>.$
    =+  [vis=(slot 6 hin) lym=(slot 7 hin)]
    =+  ^=  gin  ^-  (list slip)
        %+  turn  ((hard (list slip)) q.vis)
        |=(a=slip [[%lunt p.a] q.a])
    %=  +>.$
      ask    (weld ((hard (list slip)) q.vis) ask)
      q.ras  q.lym
    ==
  ::
  ++  pass                                                ::  try redirect
    |=  [pac=pact ced=cred] 
    ^-  (unit purl)
    =+  sam=!>(pac ced)
    =+  gat=(slap ras [%cnbc %miss])
    =+  pro=(slam gat sam)
    ?:  =(~ q.pro)  ~
    =+  vur=(slot 3 pro)
    [~ ((hard purl) q.vur)] 
  ::
  ++  post                                                ::  handle post
    |=  [zab=scab ced=cred mot=moth]
    ^+  +>
    ?>  ?=(^ r.mot)
    =+  cot=(need (~(get by q.mot) %content-type))
    =+  ^=  guz  ^-  (unit quay)
        ?.  =(cot ~['application/x-www-form-urlencoded'])  ~
        =+  vex=((full yquy:epur) [1 1] (trip q.u.r.mot))
        ?~  q.vex  ~
        [~ p.u.q.vex]
    %-  send
    ?~  guz
      [%post p.r.zab /application/octet-stream/ u.r.mot]  ::  XX parse cot
    [%form p.r.zab u.guz]
  ::
  ++  went                                                ::  handle http
    |=  [zab=scab ced=cred mot=moth]
    ^+  +>
    =+  sek=(roil [who now (shax (mix p.zab now)) ced] zab ras)
    =+  ^=  rep
        :-  %that
        ?~  sek
          [%raw [404 ~ [~ (tact "http error 404 at {<now>}")]]]
        u.sek
    +>.$(saw [rep saw])
  ::
  ++  wist                                                ::  handle note
    ^+  .
    ?.  ?=(%ht -.nut)
      ?>  ?=([%lunt *] pax)
      (send [%note t.pax nut])
    =>  ?:(=(%post p.r.nut) (post +.nut) .)
    ?>  ?=(%ht -.nut)
    (went p.nut q.nut r.nut)
  ::
  ++  zing                                                ::  resolve
    ^-  bowl
    [saw [~ (weld ask hup) ..$]]
  --
::
++  polo                                                  ::  prompt
  |=  [pim=prom pro=tape use=tape]
  |*  [rul=_rule woo=||(* bowl)]
  ^-  bowl 
  :+  ~  ~
  :-  :~  [[%polo ~] [%up pim pro]]
      ==
  |=  [now=@da pax=path nut=note]
  ^-  bowl
  ?>  &(=([%polo ~] pax) ?=(%up -.nut))
  =+  rey=(rush p.nut rul)
  ?~  rey
    :-  [[%text ?~(use "invalid response" use)] ~]
    :-  ~
    [[[[%polo ~] [%up pim pro]] ~] ..$]
  (woo u.rey)
::
++  pomo  |=([fav=card bol=bowl] [[fav p.bol] q.bol])
++  pomp  |=([txt=tape bol=bowl] (pomo [%text txt] bol))
++  roil
  |=  [mad=scad zab=scab ras=wick]
  ^-  (unit love)
  =+  cag=`path`(flop p.r.zab)
  ?>  ?=(^ cag)
  =+  buk=i.cag
  =+  hub=(hoof stub)
  =+  ven=~(rent co ~ %da q.mad)
  =+  god=~(rent co ~ %p p.mad)
  =+  tyc=(weld (flop t.cag) `path`[%web buk god ven %cy ~])
  =+  tox=(weld (flop t.cag) `path`[%web buk god ven %cx ~])
  =<  veen
  |%
  ++  drem
    |=  axt=@ta
    ^-  (unit love)
    =+  rog=((hard meta) .^((flop `path`[axt tyc])))
    ?:  ?=([& *] rog)
      =+  dat=((hard ,@) .^((flop `path`[axt tox])))
      :-  ~
      :+  %mid
        ?+  axt  [%application %octet-stream ~]
          %html  [%text %html ~]
          %txt   [%text %plain ~]
          %css   [%text %css ~]
          %js    [%text %javascript ~]
        ==
      [(met 3 dat) dat]
    =+  mog=((hard meta) .^((flop `path`[hub tyc])))
    ?.  ?=([& *] mog)  ~
    =+  wer=(flop `path`[hub tox])
    =+  dat=((hard ,@) .^(wer))
    =+  vez=(vang & wer)
    =+  gen=(scan (trip dat) (full (ifix [gay gay] tall:vez)))
    =+  pro=(slam (slam (slap ras gen) !>(mad)) !>(zab))
    [~ ((hard love) q.pro)]
  ::
  ++  dunt
    ^-  (unit love)
    (drem %html)
  ::
  ++  veen
    |-  ^-  (unit love)
    ?:  =(tyc [buk god ven %cy])  ~
    ?~  tyc  !!
    ?~  tox  !!
    =+  rab=?~(r.q.r.zab dunt (drem u.r.q.r.zab))
    ?^  rab  rab
    $(tyc t.tyc, tox t.tox)
  --
--
