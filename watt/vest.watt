::
++  vest
  =<  tall
  &.  
  ++  tall  
    |=  tub=*clip 
    ^-  *(like gene)
    %.  tub
    ;~  pose
      (norm &)
      flat
    --
  ::
  ++  wide
    |=  tub=*clip 
    ^-  *(like gene)
    %.  tub
    ;~  pose
      (norm |)
      flat
    --
  ::
  ++  flat
    ;~  pose
      (stag %dgtr (ifix [den ned] (most ace wide)))
    ::
      %+  stag
        %dtsg
      ;~  pose
        ;~(pfix (just '0') (just 'x') hex)
        dem
        quo
        (cold & pam)
        (cold | bar)
        ;~(pfix mit sym)
        (cold %% ;~(plug mit mit))
      --
    ::
      %+  stag
        %tmbn
      ;~  pose
        (cold %flag cas)
        (cold %noun tar)
        (cold %cell ket)
        (cold %atom nat)
        (cold %null sig)
      --
    --
  ::
  ++  norm
    |=  tol=?
    =<  ;~  pose
          ;~  pfix  bar
            ;~  pose
              (rune bon %brbn expb)
              (rune cas %brcs expd)
              (rune deg %brdg expb)
              (rune dap %brdp expa)
              (rune dot %brdt expa)
              (rune pad %brpd expb)
              (rune tar %brtr expb)
            --
          --
          ;~  pfix  bon
            ;~  pose
              (rune dap %bndp expb)
              (rune del %bndl expb)
              (rune led %bnld expb)
              (rune pad %bnpd expb)
            --
          --
          ;~  pfix  cas
            ;~  pose
              (rune bar %csbr expd)
              (rune bon %csbn expb)
              (rune dap %csdp exph)
              (rune deg %csdg expc)
              (rune del %csdl expb)
              (rune dot %csdt expc)
              (rune led %csld expb)
              (rune pam %cspm expd)
              (rune sig %cssg expc)
              (rune tar %cstr expi)
              (rune zap %cszp expa)
            --
          --
          ;~  pfix  deg
            ;~  pose
              (rune dap %dgdp expb)
              (rune ket %dgkt expf)
              (rune pad %dgpd expc)
              (rune sig %dgsg expd)
              (rune tar %dgtr expd)
            --
          --
          ;~  pfix  dot
            ;~  pose
              (rune bon %dtbn expb)
              (rune cas %dtcs expa)
              (rune pad %dtpd expa)
              (rune tar %dttr expb)
            --
          --
          ;~  pfix  ket
            ;~  pose
              (rune bon %ktbn expg)
              (rune dap %ktdp expb)
              (rune pad %ktpd expb)
              (rune sig %ktsg expa)
            --
          --
          ;~  pfix  mit
            ;~  pose
              (rune bar %mtbr expp)
              (rune bon %mtbn expj)
              (rune deg %mtdg expb)
              (rune dap %mtdp expk)
              (rune dot %mtdt expb)
              (rune ket %mtkt expf)
              (rune pad %mtpd expc) 
              (rune sig %mtsg expq)
              (rune tar %mttr expa)
            --
          --
          ;~  pfix  pam
            ;~  pose
              (rune bon %pmbn expx)
              (rune deg %pmdg expx)
              (rune dap %pmdp expx)
              (rune dot %pmdt expe)
              (rune pad %pmpd expx)
            --
          --
          ;~  pfix  sig
            ;~  pose
              (rune bar %sgbr expb)
              (rune bon %sgbn expb)
            --
          --
        --
    &.
    ::
    ++  boot
      %+  cook
        |=([a=& b=? c=*term d=*gene] [c a b d])
      ;~  plug
        (cold & (just '+'))
        ;~  gunk 
          ;~(pose (cold | (just '-')) (cold & (just '+')))
          sym
          loaf
        --
      --
    ++  wing
      %+  cook
        |=(a=*(list <[p=*term q=*foot]>) (~(gas in *(map term foot)) a))
      (more muck boot)
    ::
    ++  nail  sym 
    ++  rope  
      |=  tub=*clip 
      ^-  *(like twig)
      %.  tub
      ;~(plug nail ;~(pose ;~(pfix dot rope) (easy ~)))
    ::
    ++  rune
      |*  [dif=rule tuq=* har=expa]
      ;~  pfix
        dif
        %+  stag
          tuq
        =+  dur=(ifix [lep pel] $:har(tol |))
        ?:  tol
          ;~(pose ;~(pfix gap $:har(tol &)) dur)
        dur
      --
    ::
    ++  glop  (glue mash)
    ++  gunk  (glue muck)
    ++  hank  (most muck loaf)
    ++  loaf  ?:(tol tall wide)
    ++  mash  ?:(tol gap ;~(plug com ace))
    ++  muck  ?:(tol gap ace)
    ++  rack  (most mash ;~(gunk loaf loaf))
    ++  expa  |.(loaf)
    ++  expb  |.(;~(gunk loaf loaf))
    ++  expc  |.(;~(gunk loaf loaf loaf))
    ++  expd  |.(hank)
    ++  expe  |.(wing)
    ++  expf  |.(;~(gunk loaf loaf loaf loaf))
    ++  expg  |.(;~(gunk sym loaf))
    ++  exph  |.(;~(gunk loaf rack))
    ++  expi  |.(;~(gunk loaf hank))
    ++  expj  |.(;~(gunk rope rack))
    ++  expk  |.(;~(gunk loaf ;~(plug loaf (easy ~)))) 
    ++  expp  |.(;~(gunk rope loaf rack))
    ++  expq  |.(;~(gunk rope loaf loaf))
    ++  expx  |.(;~(gunk loaf wing))
    ==
  ==
