^<  mine.
  :+  ~
    [%watt 271]
  :~
    [%vade !=(vade)]
    [%vane !=(vane)]
    [%club !=(club)]
  ==
|*
  ::  Tier 1, light math
::::
::
    add
  ^<  mine.[!=(->) %add ~]
  =+  [a=@ b=@]
  |=
  ^-  @
  ?:  .=(0 a)
    b
  $(a (dec a), b .^(b))
::::
::
    bex
  ^<  mine.[!=(->) %bex ~] 
  =+  [a=@]
  |=
  ^-  @
  ?:  .=(0 a)
    1
  (mul 2 $(a (dec a)))
::::
::
    dec
  ^<  mine.[!=(->) %dec ~] 
  =+  [a=@]
  |=
  ^-  @
  ^>  bean.%dec
  ?<  .=(0 a)
  =+  b=@
  |-
  ?:  .=(a .^(b))
    b
  $(b .^(b))
::::
::
    div
  ^<  mine.[!=(->) %div ~] 
  =+  [a=@ b=@]
  |=
  ^-  @
  ^>  bean.%div
  ?<  .=(0 b)
  =+  c=@
  |-
  ?:  (lth a b)
    c
  $(a (sub a b), c .^(c))
::::
::
    fib
  =+  a=@
  |=
  ^>  memo
  ?-  a
    0   0
    1   1
    *   (add $(a (sub a 1)) $(a (sub a 2)))
  ==
::::
::
    gte
  ^<  mine.[!=(->) %gte ~] 
  =+  [a=@ b=@]
  |=
  ^-  ?
  !(lth a b)
::::
::
    gth
  ^<  mine.[!=(->) %gth ~] 
  =+  [a=@ b=@]
  |=
  ^-  ?
  !(lte a b)
::::
::
    lte
  ^<  mine.[!=(->) %lte ~] 
  =+  [a=@ b=@]
  |=
  ?|(.=(a b) (lth a b))
::::
::
    lth
  ^<  mine.[!=(->) %lth ~] 
  =+  [a=@ b=@]
  |=
  ^-  ?
  ?&
    !.=(a b)
  ::
    |-
    ?:  .=(0 a)
      &
    ?:  .=(0 b)
      |
    $(a (dec a), b (dec b))
  ==
::::
::
    mod
  ^<  mine.[!=(->) %mod ~] 
  =+  [a=@ b=@]
  |=
  ^-  @
  ?<  .=(0 b)
  (sub a (mul b (div a b)))
::::
::
    mul
  ^<  mine.[!=(->) %mul ~] 
  =+  [a=@ b=@]
  |=
  ^-  @
  =+  c=^-(@ 0)
  |-
  ?:  .=(0 a)
    c
  $(a (dec a), c (add b c))
::::
::
    nop
  =+  *
  |:
  -<
::::
::
    sub
  ^<  mine.[!=(->) %sub ~] 
  =+  [a=@ b=@]
  |=
  ^-  @
  ?:  .=(0 b)
    a
  $(a (dec a), b (dec b))
::
::::  
  ::  Tier 2, lists and units
::::
::
    bind
  =+  [*(unit) &*]
  |:
  =>  .(-< [a=-<- b=-<+])
  ?~  a
    ~
  [~ u=(b u.a)]
::::
::
    clap
  =+  [a=*(unit) b=*(unit) c==+(^ |=(-<-))]
  |:
  ?~  a
    b
  ?~  b
    a
  [~ u=(c u.a u.b)]
::::
::
    drop
  =+  a=*(unit)
  |:
  ?~  a
    ~
  [i=u.a t=~]
::::
::
    flop
  =+  a=*(list)
  |:
  ^-  a
  =+  b=`a`~
  |-
  ?~  a
    b
  $(a t.a, b [i.a b])
::::
::
    lent
  =+  a=*(list)
  |=
  ^-  @
  =+  b=@
  |-
  ?~(a b $(a t.a, b .^(b)))
::::
::
    levy
  =+  [a=*(list) b==+(* |=(.?(-<)))]
  |:  |-
  ^-  ?
  ?~  a
    &
  ?:  (b i.a)
    $(a t.a)
  |
::::
::
    lien
  =+  [a=*(list) b==+(* |=(.?(-<)))]
  |:  |-
  ^-  ?
  ?~  a
    |
  ?:  (b i.a)
    &
  $(a t.a)
::::
::
    list
  =+  a=&*
  |:
  =+  b=*
  |=
  ?-  b
    ~           ~
    [i=* t=*]   [i=(a i.b) t=$(b t.b)]
  ==
::::
::
    need
  =+  a=*(unit)
  |:
  ?~  a
    !!
  u.a
::::
::
    reel
  =+  [a=*(list) b==+([* *] |=(-<+))]
  |:  |-
  ^-  -<+.b
  ?~  a
    -<+.b
  $:b(-<- i.a, -<+ $(a t.a))
::::
::
    roll
  =+  [a=*(list) b==+([* *] |=(-<+))]
  |:  |-
  ^-  -<+.b
  ?~  a
    -<+.b
  $(a t.a, b b(-<+ $:b(-<- i.a)))
::::
::
    skim
  =+  [a=*(list) b==+([p=*] |=(.?(p)))]
  |:  |-
  ^-  a
  ?~  a
    ~
  ?:((b i.a) [i.a $(a t.a)] $(a t.a))
::::
::
    skip
  =+  [a=*(list) b==+([p=*] |=(.?(p)))]
  |:  |-
  ^-  a
  ?~  a
    ~
  ?:((b i.a) $(a t.a) [i.a $(a t.a)])
::::
::
    snag
  =+  [a=@ b=*(list)]
  |:  |-
  ?~  b
    !!
  ^-  i.b
  ?:  .=(0 a)
    i.b
  $(b t.b, a (dec a))
::::
::
    sort
  =+  [a=*(list) b==+([p=* q=*] |=(.=(p q)))]
  |:  |-
  ^-  a
  ?~  a
    ~
  %+  weld
    $(a (skim ^@(a) =+(* |:((b -< i.a)))))
  [i.a $(a (skim ^@(a) =+(* |:((b i.a -<)))))]
::::
::
    tree
  =+  a=&*
  |:
  =+  b=*
  |=
  ?-  b
    ~               ~
    [n=* l=* r=*]   [n=(a n.b) l=$(b l.b) r=$(b r.b)]
  ==
::::
::
    turn
  =+  [a=*(list) b=&*]
  |:  |-
  ?~  a
    ~
  [(b i.a) $(a t.a)]
::::
::
    unit
  =+  a=&*
  |:
  =+  b=*
  |=
  ?-  b
    ~       ~
    [~ *]   [~ u=(a +.b)]
  ==
::::
::
    weld
  =+  [a=*(list) b=*(list)]
  |:  |-
  ^-  b
  ?~  a
    b
  [i.a $(a t.a)]
::
::::
  ::  Tier 3, noun surgery
::::
::
    axis  &@
    bloq  &@
::::
::
    cap
  ^<  mine.[!=(->) %cap ~] 
  =+  [a=*axis]
  |=
  ^-  *<2 3>
  ?-  a
    2       2
    3       3
    *<0 1>  !!
    *       $(a (div a 2))
  ==
::::
::  
    cat
  ^<  mine.[!=(->) %cat ~] 
  =+  [a=*bloq b=@ c=@]
  |=
  (add (lsh a (met a b) c) b)
::::
::
    con
  ^<  mine.[!=(->) %con ~]
  =+  [a=@ b=@]
  |=
  =+  [c=`@`0 d=`@`0]
  |-
  ?:  ?&(.=(0 a) .=(0 b))
    d
  %=  $
    a   (rsh 0 1 a)
    b   (rsh 0 1 b)
    c   .^(c)
    d   (add d (lsh 0 c ?&(.=(0 (end 0 1 a)) .=(0 (end 0 1 b)))))
  ==
::::
::
    cut
  ^<  mine.[!=(->) %cut ~]
  =+  [a=*bloq [b=@ c=@] d=@]
  |=
  (end a c (rsh a b d))
::::
::
    dis
  ^<  mine.[!=(->) %dis ~]
  =+  [a=@ b=@]
  |=
  =+  [c=`@`0 d=`@`0]
  |-
  ?:  ?|(.=(0 a) .=(0 b))
    d
  %=  $
    a   (rsh 0 1 a)
    b   (rsh 0 1 b)
    c   .^(c)
    d   (add d (lsh 0 c ?|(.=(0 (end 0 1 a)) .=(0 (end 0 1 b)))))
  ==
::::
::
    dor
  ^<  mine.[!=(->) %dor ~]
  =+  [a=* b=*]
  |=
  ^-  ?
  ?:  .=(a b)
    &
  ?.  ?=(@ a)
    ?.  ?=(@ b)
      ?:  .=(-.a -.b)
        $(a +.a, b +.b)
      $(a -.a, b -.a)
    |
  ?.  ?=(@ b)
    &
  (lth a b)
::::
::
    end 
  ^<  mine.[!=(->) %end ~]
  =+  [a=*bloq b=@ c=@]
  |=
  (mod c (bex (mul (bex a) b)))
::::
::
    gor
  ^<  mine.[!=(->) %gor ~]
  =+  [a=* b=*]
  |=
  ^-  ?
  =+  [c=(mug a) d=(mug b)]
  ?:  .=(c d)
    (dor a b)
  (lth c d)
::::
::
    lsh
  ^<  mine.[!=(->) %lsh ~]
  =+  [a=*bloq b=@ c=@]
  |=
  (mul (bex (mul (bex a) b)) c)
::::
::
    mix
  ^<  mine.[!=(->) %mix ~]
  =+  [a=@ b=@]
  |=
  =+  [c=`@`0 d=`@`0]
  |-
  ?:  ?&(.=(0 a) .=(0 b))
    d
  %=  $
    a   (rsh 0 1 a)
    b   (rsh 0 1 b)
    c   .^(c)
    d   (add d (lsh 0 c .=((end 0 1 a) (end 0 1 b))))
  ==
::::
::
    met
  ^<  mine.[!=(->) %met ~]
  =+  [a=*bloq b=@]
  |=
  =+  c=`@`0
  |-
  ?:  .=(0 b)
    c
  $(b (rsh a 1 b), c .^(c))
::::
::
    mas
  ^<  mine.[!=(->) %mas ~]
  =+  [a=*axis]
  |=
  ^-  *axis
  ?-  a
    1   !!
    2   1
    3   1
    *   (add (mod a 2) (mul $(a (div a 2)) 2))
  ==
::::
::
    mug
  ^<  mine.[!=(->) %mug ~]
  =+  a=*
  |=
  ^-  @
  =<  $
  |*
  ::::
  ::
      $
    ?.  ?=(@ a)
      =+  [b=$(a -.a) c=$(a +.a)]
      =+  d=(mix b (add (end 5 1 (lsh 3 1 c)) (rsh 3 3 c)))
      |-
      =+  e=(dis 0x7fffffff (chum d))
      ?:  .=(0 e)
        $(d .^(d))
      e
    =+  b=`@`0x18d0a625
    |-
    =+  c=(met 5 a)
    =+  d=`@`0
    =+  e=b
    |-
    ?:  .=(d c)
      =+  f=(dis 0x7fffffff e)
      ?:  .=(0 f)
        $$(b .^(b))
      f
    $(d .^(d), e (chum (mix e (cut 5 [1 d] a))))
  ::::
  ::
      chum
    =+  [b=@]
    |=
    =-  (end 5 1 -)
    (dice 3 (dice 2 (dice 1 (dice 0 b))))
  ::::
  ::
      dice
    =+  [b=@ c=@]
    |=
    (mix c (sbox (cut 3 [1 b] c))) 
  ::::
  ::
      sbox
    =+  [b=@]
    |=
    =-  (cut 5 [1 b] -)
    0x45bd2fb7\ /eec357b8\ /2ae29f22\ /bb412e03\ /08445336\ /52ca46d8\
     /76d680b9\ /b9e06f14\ /6f8b9c36\ /a53ef834\ /e3533b5b\ /c5cd423e\
     /0cfd4d14\ /12924b1a\ /3e77ee18\ /1804e2a5\ /75d9c4e1\ /6d4b9a3e\
     /5d56c751\ /12ad4163\ /b9df9e63\ /fbfbdac7\ /54464f51\ /4b72be7f\
     /180d4405\ /1b456773\ /5fb51d73\ /2d4774c2\ /381585e2\ /0209e49e\
     /52c0256a\ /6deac45d\ /3f55045c\ /40b92e52\ /5242aa7a\ /a47337b1\
     /bc816aba\ /4bbb0ecf\ /f82cb4ad\ /0bc99c47\ /78a5c74c\ /9182f20d\
     /7b941dc3\ /ff307dd2\ /fc36ca9d\ /0c32efad\ /b1c38ca7\ /b799e1be\
     /fd0dbfee\ /9887957d\ /70186f9e\ /31325eca\ /48f0ac44\ /c7eb6480\
     /ab1f9401\ /167cf21d\ /dce5b8fa\ /121c9f64\ /e95eacb5\ /c50dca03\
     /8b29fd75\ /875f1bb9\ /261c8938\ /3bf794dc\ /e4395998\ /1b06df20\
     /19c11553\ /eefbdb16\ /54447c57\ /f54a4761\ /bdf13b8c\ /88502921\
     /72553aee\ /c63a1157\ /03a1f284\ /f5b59c3c\ /96923c87\ /e2c0936d\
     /4f112d16\ /0f4c5845\ /1055a07d\ /d82afef8\ /72fb1b49\ /0fa54e14\
     /637d01f5\ /7fe89a8b\ /0617eda8\ /4354f3e7\ /712f6316\ /9036b901\
     /111a1094\ /70c78bcc\ /28329ab3\ /fdf004ca\ /2026a870\ /85ac98d2\
     /3f52439e\ /e030c8ed\ /a7589924\ /05af69ed\ /566dd09c\ /4237062e\
     /e9fc60eb\ /c415321c\ /ac4fc47f\ /b265b0ee\ /95cdc493\ /3beb6b4d\
     /e777884a\ /370f9e65\ /6a52c674\ /62dc97c6\ /e5d5238e\ /b6315b6a\
     /c845a9e2\ /026c78ab\ /13beec00\ /b3ed287a\ /a01ebf2b\ /b5c0da93\
     /deeedce8\ /3e5ec58d\ /e83350f0\ /e6ffe2fb\ /a3765510\ /37f42e48\
     /474c90cf\ /78eea53d\ /d10faca2\ /a9417956\ /7c600063\ /14187d04\
     /faffdd23\ /44844992\ /61f4a52d\ /a234566b\ /a46b9dd7\ /1a9d0bd3\
     /cf3e4033\ /b3bcada3\ /00f3ef4e\ /7367a5f9\ /5e24070c\ /1b973827\
     /5dcd3652\ /95b1eb1f\ /035857a4\ /c090c33b\ /5eba8280\ /475d8534\
     /7fb44be7\ /3bffc889\ /dbaf1fa2\ /948328b1\ /56a8a01c\ /6717fa39\
     /4c41d2c6\ /b3275702\ /51634e92\ /51a573dd\ /d3ea1b9d\ /be74ea0b\
     /9093fa5f\ /d2e1d0d1\ /8a1fd93a\ /8f1d0a6e\ /b7a9e735\ /deb0aa12\
     /48d76271\ /e808ec02\ /8e498a90\ /83539726\ /86c7973e\ /44337e55\
     /81f4f7ca\ /3052b642\ /873b540e\ /68d8efa2\ /18494012\ /decc1486\
     /29079afa\ /b81a7b1d\ /63dd42a0\ /3cab1b6f\ /713fffa2\ /08835b42\
     /6dd6f5f8\ /65a14a04\ /5eab8eac\ /2fe5b7f9\ /bbc96404\ /f593e52b\
     /b2078d1a\ /f192cbcd\ /c5e102b2\ /9187837c\ /bddd8a18\ /5caf8a56\
     /3b7aa391\ /88fd1802\ /1e458e1d\ /5f606388\ /513ce98d\ /aed1093b\
     /ae87124c\ /f537ea14\ /0f4bb673\ /cfbeae41\ /123e5e9f\ /e14077a2\
     /40acd8dc\ /103b81cf\ /683fa466\ /9b9e2cec\ /23171ec6\ /2ba16060\
     /d3bfabae\ /dd68fcd2\ /f60ee7be\ /f44c697e\ /7990bd4e\ /3ce83919\
     /bc457a9d\ /5022a039\ /a158b454\ /80722ba6\ /87652d85\ /e2ddee5a\
     /5c43205e\ /c62f6816\ /4c680e34\ /151f4b17\ /9570fa83\ /eb9f040e\
     /22b9bd9c\ /d156e14b\ /3a30bdd8\ /f00fc9e2\ /62bcfdef\ /da4952df\
     /0c100eda\ /ccffce0a\ /1994625d\ /b0cd88c6\ /766ff6ec\ /7c8f831a\
     /7824fff6\ /dee5b579\ /99267e88\ /f8820121\ /6220ecc5\ /f40f7a8d\
     /fe8c511a\ /a9afcd63\ /ba9cd382\ /352598d8
  ::
  ::::
  ==
::::
::
    peg
  ^<  mine.[!=(->) %peg ~]
  =+  [a=*axis b=*axis]
  |=
  ^-  *axis
  ?-  b
    1   a
    2   (mul a 2)
    3   .^((mul a 2))
    *   (add (mod b 2) (mul $(b (div b 2)) 2))
  ==
::::
::
    rap
  ^<  mine.[!=(->) %rap ~]
  =+  [a=*bloq b=*(list &@)]
  |=
  ^-  @
  ?~  b
    0
  (cat a i.b $(b t.b))
::::
::
    rip
  ^<  mine.[!=(->) %rip ~]
  =+  [a=*bloq b=@]
  |=
  ^-  *(list &@)
  ?:  .=(0 b)
    ~
  [(end a 1 b) $(b (rsh a 1 b))]
::::
::
    rsh
  ^<  mine.[!=(->) %rsh ~]
  =+  [a=*bloq b=@ c=@]
  |=
  (div c (bex (mul (bex a) b)))
::::
::
    vor
  ^<  mine.[!=(->) %vor ~]
  =+  [a=* b=*]
  |=
  ^-  ?
  =+  [c=(mug (mug a)) d=(mug (mug b))]
  ?:  .=(c d)
    (dor a b)
  (lth c d)
::
::::
  ::  Tier 4, associative containers 
::::
::
    pool  =+(a=&* |:((tree a)))
    book  =+([a=&* b=&*] |:((tree &[p=*a q=*b])))
::::
::
    in
  ^<  mine.[!=(->) %in ~]
  =+  a=*(pool)
  |%
  ::::
  ::
      all
    =+  b==+(* |=(?))
    |:  |-
    ^-  ?
    ?~  a
      &
    ?&((b n.a) $(a l.a) $(a r.a))
  ::::
  ::
      any
    =+  b==+(* |=(?))
    |:  |-
    ^-  ?
    ?~  a
      |
    ?|((b n.a) $(a l.a) $(a r.a))
  ::::
  ::
      gas
    =+  b=*(list)
    |:  |-
    ^-  a
    ?~  b
      a
    $(b t.b, a (put i.b))
  ::::
  ::
      has
    ^<  mine.[!=(->) %has ~]
    =+  b=*
    |:  |-
    ^-  ?
    ?~  a
      |
    ?:  .=(b n.a)
      &
    ?:  (gor b n.a)
      $(a l.a)
    $(a r.a)
  ::::
  ::
      put
    ^<  mine.[!=(->) %put ~]
    =+  b=*
    |:  |-
    ^-  a
    ?~  a
      [b ~ ~]
    ?:  .=(b n.a)
      a
    ?:  (gor b n.a)
      =+  c=$(a l.a)
      ?>  ?=(^ c)
      ?:  (vor n.a n.c)
        [n.a c r.a]
      [n.c l.c [n.a r.c r.a]]
    =+  c=$(a r.a)
    ?>  ?=(^ c)
    ?:  (vor n.a n.c)
      [n.a l.a c] 
    [n.c [n.a l.a l.c] r.c]
  ::::
  ::
      tap
    =+  b=*(list)
    |:  |-
    ?~  a
      b
    $(a r.a, b [n.a $(a l.a)])
  ::
  ::::
  ==
::::
::
    by
  ^<  mine.[!=(->) %by ~]
  =+  a=*(book)
  |%
  ::::
  ::
      all
    =+  b==+(* |=(?))
    |:  |-
    ^-  ?
    ?~  a
      &
    ?&((b q.n.a) $(a l.a) $(a r.a))
  ::::
  ::
      any
    =+  b==+(* |=(?))
    |:  |-
    ^-  ?
    ?~  a
      |
    ?|((b q.n.a) $(a l.a) $(a r.a))
  ::::
  ::
      gas
    ^<  mine.[!=(->) %gas ~]
    =+  b=*(list &[p=* q=*])
    |:  |-
    ^-  a
    ?~  b
      a
    $(b t.b, a (put p.i.b q.i.b))
  ::::
  ::
      get
    =+  b=*
    |:  |-
    ?~  a
      ~
    ?:  .=(b p.n.a)
      [~ u=q.n.a]
    ?:  (gor b p.n.a)
      $(a l.a)
    $(a r.a)
  ::::
  ::
      put
    ^<  mine.[!=(->) %put ~]
    =+  [b=* c=*]
    |:
    ^-  a
    ?~  a
      [[b c] ~ ~]
    ?:  .=(b p.n.a)
      ?>  .=(c q.n.a)
      a
    ?:  (gor b p.n.a)
      =+  d=$(a l.a)
      ?>  ?=(^ d)
      ?:  (vor p.n.a p.n.d)
        [n.a d r.a]
      [n.d l.d [n.a r.d r.a]]
    =+  d=$(a r.a)
    ?>  ?=(^ d)
    ?:  (vor p.n.a p.n.d)
      [n.a l.a d] 
    [n.d [n.a l.a l.d] r.d]
  ::::
  ::
      tap
    =+  b=*(list &[p=* q=*])
    |:
    ?~  a
      b
    $(a r.a, b [n.a $(a l.a)])
  ::
  ::::
  ==
::
::::
  ::  Tier 5, parsing and printing
::::
::
    byte  &@
    tape  (list byte)
    rock  &@
    term  &@
    wall  (list tape)
::::
::
    steg
  =+  yob=@
  |=
  (~(fly club ~(dem golf yob)) 75)
::::
::
    ball
  |?
    ^+(@ *rock)
    [%duct p=*tape]
    [%grip p=[p=*rock q=*rock] q=*(list ball)]
    [%meld p=*(list ball)]
  ==
::::
::
    club
  ^<    mine.
      :+  !=(->) 
        %club 
      :~
        [%fly !=(fly)]
        [%ram !=(ram)]
        [%wit !=(wit)]
      ==
  =+  pup=*ball
  |*
  ::::
  ::        fly: print tall
      fly
    ^<  mine.[!=(->) %fly ~]
    =+  bor=@
    |=
    ^-  *wall
    =+  col=@
    =>  |*
        ::::
        ::        ind: indent tape
            ind
          =+  tep=*tape
          |=
          ^-  *tape
          ?:  .=(0 col)
            tep
          $(col (dec col), tep [' ' tep])
        ==
    ::::
    =+  lux=*wall
    |-  ^-  *wall
    ?.  ?&(?=([%grip *] pup) (gte (add col wit) bor))
      [(ind ram) lux]
    :-  (ind (rip 3 p.p.pup))
    |-  ^-  *wall
    ?~  q.pup
      [(ind (rip 3 q.p.pup)) lux]
    %=  $$
      pup   i.q.pup
      lux   $(q.pup t.q.pup)
      col   ?:((gte col (mul 2 (div bor 3))) 0 (add 2 col))
    ==
  ::::
  ::        ram: print wide
      ram
    ^-  *tape
    ?-    pup 
    ::
        @           (rip 3 pup)
        [%meld *]   (reel p.pup =+([*ball *tape] |=((weld ram(pup -<-) -<+))))
        [%duct *]   p.pup 
    ::
        [%grip *]
      %+  weld
        (rip 3 p.p.pup)
      |-  ^-  *tape
      ?-  q.pup
        ~       (rip 3 q.p.pup)
        [* ~]   (weld ram(pup i.q.pup) (rip 3 q.p.pup))
        ^       (weld ram(pup i.q.pup) `*tape`[' ' $(q.pup t.q.pup)])
      ==
    ==
  ::::
  ::      wit: width rammed
      wit
    ^-  @
    ?-    pup
        @           (met 3 pup)
        [%duct *]   (lent p.pup)
    ::
        [%grip *]
      %+  add
        (add (met 3 p.p.pup) (met 3 q.p.pup))
      %+  roll
        q.pup
      =+  [*ball @]
      |=((add ?:(.=(0 -<+) 0 1) (add -<+ wit(pup -<-))))
    ::
        [%meld *]   (roll p.pup =+([*ball @] |=((add -<+ wit(pup -<-)))))
    == 
  ::
  ::::
  ==
::::
::
    golf
  =+  nut=*
  |%
  ::::
  ::
      dem
    (num 10 =+(@ |=((add '0' -<))))
  ::::
  ::
      gut
    |-  ^-  *ball
    ?-  nut
        @   unt(nut nut)
        ^
      :+  %grip
        ['[' ']']
      |-  ^-  *(list ball)
      ?-  nut
        [* @]   [$$(nut -.nut) $$(nut +.nut) ~]
        [* ^]   [$$(nut -.nut) $(nut +.nut)]
      ==
    ==
  ::::
  ::
      ham
    (num 16 =+(@ |=(?:((lth 10 -<) (add '0' -<) (add 'a' -<)))))
  ::::
  ::
      loc
    ^-  *ball
    ?>  ?=([p=@ q=[p=[p=@ q=@] q=[p=@ q=@]]] nut)   :: should be static
    :+  %grip
      ['<' '>']
    =+  ^=  rov
        :~  [%grip ['[' ']'] dem(nut p.p.q.nut) dem(nut q.p.q.nut) ~]
            [%grip ['[' ']'] dem(nut p.q.q.nut) dem(nut q.q.q.nut) ~]
        ==
    ?:  .=(%% p.nut)
      rov
    [p.nut rov]
  ::::
  ::
      num
    =>  ^:(@ nut)
    =+  [zel=@ hoz==+(@ |=(@))]
    |:
    ^-  [%duct p=*tape]
    :-  %duct
    ?:  .=(0 nut)
      [(hoz 0) ~]
    =+  biq=`*tape`~
    |-  ^-  *tape
    ?:  .=(0 nut)
      biq
    $(nut (div nut zel), biq [(hoz (mod nut zel)) biq])
  ::::
  ::
      tis
    =>  ^:(*tape nut)
    [%duct p=nut]
  ::::
  ::
      unt
    =>  ^:(@ nut)
    ^-  [%duct p=*tape]
    ?:  ?&  (gte (met 3 nut) 2)
            |-
            ?:  .=(0 nut)
              &
            =+  vis=(end 3 1 nut)
            =+  gor=(rsh 3 1 nut)
            ?&(?|(.=('-' vis) ?&((gte vis 'a') (lte vis 'z'))) $(nut gor))
        ==
      =<([- ['%' +]] vam)
    ?:  (lte (met 3 nut) 2)
      dem
    ham
  ::::
  ::
      vam
    =>  ^:(@ nut)
    ^-  [%duct p=*tape]
    :-  %duct
    |-  ^-  *tape
    ?:  .=(0 nut)
      ~
    [(end 3 1 nut) $(nut (rsh 3 1 nut))]
  ::
  ::::
  ==
::
::::
  ::  Tier 6, Watt in Watt
::::
::
    comb
  ^<  mine.[!=(->) %comb ~]
  =+  [mal=*tool buz=*tool]
  |=
  ^>  bean.%comb
  ^-  *tool
  ?:  ?=([0 *] mal)
    ?:  ?=([0 *] buz)
      [0 (peg p.mal p.buz)]
    ?:  ?=([2 [0 *] [0 *]] buz)
      [2 [0 (peg p.mal p.p.buz)] [0 (peg p.mal p.q.buz)]]
    [7 mal buz]
  ?:  ?=([^ [0 1]] mal)
    [8 p.mal buz]
  ?:  .=([0 1] buz)
    mal
  [7 mal buz]
::::
::
    cons
  ^<  mine.[!=(->) %cons ~]
  =+  [vur=*tool sed=*tool]
  |=
  ^>  bean.%cons
  ^-  *tool
  ?:  ?=([[0 *] [0 *]] -<)
    ?:  ?&(.=(.^(p.vur) p.sed) .=((div p.vur 2) (div p.sed 2)))
      [0 (div p.vur 2)]
    [vur sed]
  ?:  ?=([[1 *] [1 *]] -<)
    [1 p.vur p.sed]
  [vur sed] 
::::
::
    eith
  ^<  mine.[!=(->) %eith ~]
  =+  [hoz=*type bur=*type]
  |=
  ^-  *type
  ?:  .=(%blot hoz)
    bur
  ?:  .=(%blot bur)
    hoz
  [%fork hoz bur]
::::
::
    flan
  ^<  mine.[!=(->) %flan ~]
  =+  [bos=*tool nif=*tool]
  |=
  ^-  *tool
  ?-    bos
      [1 1]   bos
      [1 0]   nif
      *
    ?-    nif
        [1 1]   nif
        [1 0]   bos
        *       [6 bos nif [1 1]]
    ==
  ==
::::
::
    flip
  ^<  mine.[!=(->) %flip ~]
  =+  [hel=*tool]
  |=
  [6 hel [1 1] [1 0]]
::::
::
    flor
  ^<  mine.[!=(->) %flor ~]
  =+  [bos=*tool nif=*tool]
  |=
  ^-  *tool
  ?-  bos
      [1 1]   nif
      [1 0]   bos
      *
    ?-  nif
        [1 1]   bos
        [1 0]   nif
        *       [6 bos [1 0] nif]
    ==
  ==
::::
::
    gene
  |?
    ^+(@ *term)
    [~ p=^+(@ *axis)]
  ::
    [%bndv p=*gene q=*gene]                                     ::  =<  bindev
    [%bnnp p=*gene q=*gene]                                     ::  =-  binnup
    [%bnpd p=*gene q=*gene]                                     ::  =+  binpod
    [%bnvd p=*gene q=*gene]                                     ::  =>  binved
  ::
    [%brbn p=*gene]                                             ::  |=  barbin
    [%brdg p=*gene]                                             ::  |:  bardig
    [%brdv p=*gene q=*gene]                                     ::  |<  bardev
    [%brks p=*(list gene)]                                      ::  |?  barkes
    [%brmt p=*(list &[p=*term q=*gene])]                        ::  |%  barmit
    [%brnp p=*gene]                                             ::  |-  barnup
    [%brrs p=*(list &[p=*term q=*gene])]                        ::  |*  barras
    [%brsg p=*gene q=*(list gene)]                              ::  |~  barsig
    [%brvd p=*gene q=*gene]                                     ::  |>  barved
  ::
    [%dght p=*gene q=*gene r=*gene s=*gene]                     ::  :^  dighat
    [%dgnp p=*gene q=*gene]                                     ::  :-  dignup
    [%dgpd p=*gene q=*gene r=*gene]                             ::  :+  digpod
    [%dgrs p=*(list gene)]                                      ::  :*  digras
    [%dgsg p=*(list gene)]                                      ::  :~  digsig
  :: 
    [%dtbn p=*gene q=*gene]                                     ::  .=  dotbin
    [%dtht p=*gene]                                             ::  .^  dothat
    [%dtks p=*gene]                                             ::  .?  dotkes
    [%dtrs p=*gene q=*gene]                                     ::  .*  dotras
    [%dtsg p=*]                                                 ::  .~  dotsig
  ::
    [%hpbn p=*gene]                                             ::  !=  hopbin
    [%hpcb p=*spot q=*gene]                                     ::  !_  hopcab
    [%hpdg p=*gene]                                             ::  !:  hopdig
    [%hphp ~]                                                   ::  !!  hophop
    [%hpmt ~]                                                   ::  !%  hopmit
  ::
    [%htbn p=*term q=*gene]                                     ::  ^=  hatbin
    [%htdg p=*gene q=*gene]                                     ::  ^:  hatdig
    [%htdv p=*<@ [p=@ q=*gene]> q=*gene]                        ::  ^<  hatdev
    [%htmt p=*gene q=*gene]                                     ::  ^%  hatmit
    [%htnp p=*gene q=*gene]                                     ::  ^-  hatnup
    [%htpd p=*gene q=*gene]                                     ::  ^+  hatpod
    [%htvd p=*<@ [p=@ q=*gene]> q=*gene]                        ::  ^>  hatved
    [%htrs p=*gene]                                             ::  ^*  hatras
    [%htsg p=*gene]                                             ::  ^~  hatsig
  ::
    [%ksbn p=*gene q=*gene]                                     ::  ?=  kesbin
    [%ksbr p=*(list gene)]                                      ::  ?|  kesbar
    [%ksdg p=*gene q=*gene r=*gene]                             ::  ?:  kesdig
    [%ksdv p=*gene q=*gene]                                     ::  ?<  kesdev
    [%ksdt p=*gene q=*gene r=*gene]                             ::  ?.  kesdot
    [%kshp p=*gene]                                             ::  ?!  keshop
    [%ksnp p=*gene q=*(list &[p=*gene q=*gene])]                ::  ?-  kesnup
    [%kspm p=*(list gene)]                                      ::  ?&  kespam
    [%ksvd p=*gene q=*gene]                                     ::  ?>  kesved
    [%ksrs p=*gene q=*(list gene)]                              ::  ?*  kesras
    [%kssg p=*gene q=*gene r=*gene]                             ::  ?~  kessig
  ::
    [%lmbn p=*<%atom %blur %cell %flag %null>]                  ::  ;=  lombin
    [%lmnp p=*gene]                                             ::  ;-  lomnup
    [%lmpd p=*gene]                                             ::  ;+  lompod
  ::
    [%mtbn p=*rope q=*(list &[p=*gene q=*gene])]                ::  %=  mitbin
    [%mtbr p=*rope q=*gene r=*(list &[p=*gene q=*gene])]        ::  %|  mitbar
    [%mtdg p=*gene q=*gene]                                     ::  %:  mitdig
    [%mtdt p=*gene q=*gene]                                     ::  %.  mitdot
    [%mtht p=*gene q=*gene r=*gene s=*gene]                     ::  %^  mithat
    [%mtnp p=*gene q=*(list gene)]                              ::  %-  mitnup  
    [%mtpd p=*gene q=*gene r=*gene]                             ::  %+  mitpod
    [%mtrs p=*gene]                                             ::  %*  mitras
    [%mtsg p=*rope q=*gene r=*gene]                             ::  %~  mitsig
  ==
::::
::
    hike 
  ^<  mine.[!=(->) %hike ~]
  =+  [axe=*axis pac=*(list &[p=*axis q=*tool])]
  |=
  ^>  bean.%hike
  ^-  *tool
  ?~  pac
    [0 axe]
  =+  zet=(skim ^@(pac) =+([p=*axis q=*tool] |=([.=(1 p)])))
  ?~  zet
    =+  tum=(skim ^@(pac) =+([p=*axis q=*tool] |=(?&(!.=(1 p) .=(2 (cap p))))))
    =+  gam=(skim ^@(pac) =+([p=*axis q=*tool] |=(?&(!.=(1 p) .=(3 (cap p))))))
    %+  cons
      %=  $
        axe (peg axe 2)
        pac (turn tum =+([p=*axis q=*tool] |=([(mas p) q])))
      ==
    %=  $
      axe (peg axe 3)
      pac (turn gam =+([p=*axis q=*tool] |=([(mas p) q])))
    ==
  ?>(?=([* ~] zet) q.i.zet)
::::
::
    look
  ^<  mine.[!=(->) %look ~]
  =+  [cog=*term dab=*(book term gene)]
  |= 
  =+  axe=`@`1
  |-
  ^-  *(unit &[p=*axis q=*gene])
  ?-  dab
      ~   ~
  ::
      [* ~ ~]   
    ?:(.=(cog p.n.dab) [~ axe q.n.dab] ~)
  ::
      [* ~ *]
    ?:  .=(cog p.n.dab)
      [~ (peg axe 2) q.n.dab]
    ?:  (gor cog p.n.dab)
      ~
    $(axe (peg axe 3), dab r.dab)
  ::
      [* * ~]
    ?:  .=(cog p.n.dab)
      [~ (peg axe 2) q.n.dab]
    ?:  (gor cog p.n.dab)
      $(axe (peg axe 3), dab l.dab)
    ~
  ::
      [* * *]
    ?:  .=(cog p.n.dab)
      [~ (peg axe 2) q.n.dab]
    ?:  (gor cog p.n.dab)
      $(axe (peg axe 6), dab l.dab)
    $(axe (peg axe 7), dab r.dab)
  ==
::::
::
    open
  ^<  mine.[!=(->) %open ~]
  =+  gen=*gene
  |=
  ^>  bean.%open
  ^-  *gene
  =<  base
  |*
  ::::
  ::
      base
    ?-    gen
        @           [%mtbn [gen ~] ~]
        [~ *]       [%mtbn [gen ~] ~]
        [%brbn *]   [%brrs [[%% p.gen] ~]]                              ::  |=
        [%brdg *]   [%brmt [[%% p.gen] ~]]                              ::  |:
        [%brnp *]   [%bnvd [%brbn p.gen] %%]                            ::  |-
        [%brsg *]                                                       ::  |~
      |-
      ?-  q.gen
          ~       !!
          [* ~]   i.q.gen
      ::
          ^
        :+  %bnpd
          $(q.gen t.q.gen)
        :+  %bnpd
          [%bnvd [~ 3] i.q.gen]
        :+  %bnpd
          [~ 8]
        :-  %brdg
        :^    %mtpd
            [%bnvd [~ 23] p.gen]
          [%mtnp [~ 10] [~ 4] ~]
        [%mtbn [[~ 22] ~] [[[~ 4] [~ 4]] ~]]
      ==
    ::
        [%brks *]                                                       ::  |?
      ?~  p.gen
        [%hphp ~]
      [%bnpd [%htrs i.p.gen] [%brbn [%ksrs [~ 4] p.gen]]]
    ::
        [%brvd *]   [%bnpd p.gen [%brbn q.gen]]
        [%bndv *]   [%bnvd q.gen p.gen]                                 ::  =<
        [%bnnp *]   [%bnpd q.gen p.gen]                                 ::  =-
        [%bnpd *]   [%bnvd [%dgnp p.gen [~ 1]] q.gen]                   ::  =+
        [%dght *]   [%dgnp p.gen [%dgnp q.gen [%dgnp r.gen s.gen]]]     ::  :^
        [%dgpd *]   [%dgnp p.gen [%dgnp q.gen r.gen]]                   ::  :+
        [%dgrs *]                                                       ::  :*
      |-
      ?-    p.gen 
          ~       [%hphp ~]
          [* ~]   i.p.gen
          ^       [%dgnp i.p.gen $(p.gen t.p.gen)]
      ==
    ::
        [%dgsg *]                                                       ::  :~
      |-
      ?~(p.gen [%dtsg ~] [%dgnp i.p.gen $(p.gen t.p.gen)])
    ::
        [%htdg *]                                                       ::  ^:
      :+  %bnpd
        q.gen
      [%bndv [~ 2] [%htpd [%bnvd [~ 3] p.gen] [~ 2]]]
    ::
        [%htmt *]                                                       ::  ^%
      :+  %bnpd 
        q.gen 
      :+  %ksnp
        [~ 2]
      [[[%bnvd [~ 3] p.gen] [~ 2]] ~]
    ::
        [%htdv *]   [%bndv [%htvd p.gen [~ 1]] q.gen]                   ::  ^<
        [%htrs *]   [%htnp [%lmbn %blur] p.gen]                         ::  ^*
        [%htsg *]   p.gen                                               ::  ^~
        [%ksbr *]                                                       ::  ?|
      |- 
      ?~(p.gen [%dtsg 1] [%ksdg i.p.gen [%dtsg 0] $(p.gen t.p.gen)])
    ::
        [%ksdv *]   [%ksdg p.gen [%hphp ~] q.gen]                       ::  ?<
        [%ksdt *]   [%ksdg p.gen r.gen q.gen]                           ::  ?.
        [%kshp *]   [%ksdg p.gen [%dtsg 1] [%dtsg 0]]                   ::  ?!
        [%ksnp *]                                                       ::  ?-
      |-
      ?~  q.gen
        [%hphp ~]
      :^    %ksdg
          [%ksbn (fist p.i.q.gen) p.gen]
        :+  %bnvd
          [%htpd p.i.q.gen p.gen]
        q.i.q.gen
      $(q.gen t.q.gen)
    ::
        [%kspm *]                                                       ::  ?&
      |-
      ?~(p.gen [%dtsg 0] [%ksdg i.p.gen $(p.gen t.p.gen) [%dtsg 1]])
    ::
        [%ksvd *]   [%ksdg p.gen q.gen [%hphp ~]]                       ::  ?>
        [%ksrs *]                                                       ::  ?*
      |-
      ?~  q.gen
        [%hphp ~]
      :^    %ksdg
          [%ksbn (fist i.q.gen) p.gen]
        [%mtnp (mast i.q.gen) p.gen ~]
      $(q.gen t.q.gen)
    ::
        [%kssg *]                                                       ::  ?~
      [%ksnp p.gen [[[%dtsg 0] q.gen] [[%lmbn %cell] r.gen] ~]]
    ::
        [%lmbn *]                                                       ::  ;=
      ?-  p.gen
        %atom   [%htnp [%dtht %dtsg 0] [%dtsg 0]]
        %blur   [%htnp [%dtrs [%dtsg 0] [%dtsg 0]] [%dtsg 0]]
        %cell   [%dgnp [%lmbn %blur] [%lmbn %blur]]
        %flag   [%htnp [%dtks %dtsg 0] [%dtsg 0]]
        %null   [%dtsg 0]
      ==
    ::
        [%lmnp *]   (mast p.gen)                                        ::  ;-
        [%lmpd *]   (fist p.gen)                                        ::  ;+
        [%mtbr *]                                                       ::  %|
      :+  %bnpd
        q.gen
      :+  %mtbn
        (weld p.gen `*rope`[[~ 2] ~])
      (turn r.gen =+([*gene *gene] |=([-<- [%bnvd [~ 3] -<+]])))
    ::
        [%mtdg *]   [%mtsg [%% ~] p.gen q.gen]                          ::  %:
        [%mtdt *]   [%mtnp q.gen [p.gen ~]]                             ::  %.
        [%mtht *]   [%mtnp p.gen q.gen r.gen s.gen ~]                   ::  %^
        [%mtnp *]                                                       ::  %-  
      ?~(q.gen [%bnvd p.gen %%] [%mtdg p.gen [%dgrs q.gen]])
    ::                                                                  ::  %+
        [%mtpd *]   [%mtnp p.gen q.gen r.gen ~]
        [%mtrs *]   [%htsg [%bnvd p.gen [%mtbn [%% ~] ~]]]              ::  %*
        [%mtsg *]   [%mtbr p.gen q.gen [[[~ 4] r.gen] ~]]               ::  %~
        *   
      gen
    ==
  ::::
  ::
      mast
    =+  gen=*gene
    |=
    ^-  *gene
    =<  pax
    |*
    ::::
    ::
        pax
      ^-  *gene
      ?-    gen
          [%htdg *]   pax(gen q.gen)
          [%htdv *]   [%htdv p.gen pax(gen q.gen)]
          [%htmt *]
        :+  %bnpd
          [%htsg [%htrs q.gen]]
        :-  %brbn
        :+  %bnvd
          [%mtbn ~ [[[~ 4] [%mtnp pax(gen q.gen) [~ 4] ~]] ~]]
        (fix(gen p.gen) 4)
      ::
          [%htnp *]   
        [%bnpd [%htsg [%htrs q.gen]] [%brbn (fix(gen p.gen) 4)]]
      ::
          [%htpd *]   pax(gen q.gen)
          [%htvd *]   [%htvd p.gen pax(gen q.gen)]
          [%mtrs *]   p.gen
          *           
        =+  bog=(open gen)
        ?.  .=(bog gen)
          pax(gen bog)
        [%bnpd [%htsg [%htrs gen]] [%brbn (fix 4)]]
      ==
    ::::
    ::
        fix
      =+  [axe=*axis]
      |=
      ^-  *gene
      ?-    gen
          [%dgnp *]
        :+  %dgnp
          $(gen p.gen, axe (peg axe 2))
        $(gen q.gen, axe (peg axe 3))
      ::
          [%htbn *]   [%htbn p.gen $(gen q.gen)]
          [%htdv *]   [%htdv p.gen $(gen q.gen)]
          [%htnp *]   $(gen p.gen)
          [%htpd *]   $(gen q.gen)
          [%htmt *]
        :+  %bnvd
          [%mtbn ~ [[[~ axe] [%mtnp $$(gen q.gen) [~ axe] ~]] ~]]
        $(gen p.gen)
      ::
          [%htvd *]   [%htvd p.gen $(gen q.gen)]
          [%mtrs *]   [%mtnp p.gen [~ axe] ~]
      ::
          *
        =+  bog=(open gen)
        ?.  .=(bog gen)
          $(gen bog)
        [%ksvd [%ksbn gen [~ axe]] [~ axe]]
      ==
    ==
  ::::
  ::
      fist
    =+  gen=*gene
    |=
    ^-  *gene
    ?-  gen
        [%dgnp *]   [%dgnp $(gen p.gen) $(gen q.gen)]
        [%dtsg *]   gen
        [%htbn *]   [%htbn p.gen $(gen q.gen)]
        [%htdv *]   [%htdv p.gen $(gen q.gen)]
        [%htmt *]   [%htmt $(gen p.gen) $(gen q.gen)]
        [%htnp *]   $(gen p.gen)
        [%htpd *]   $(gen p.gen)
        [%htvd *]   [%htvd p.gen $(gen q.gen)]
        [%lmbn *]   gen
        [%lmpd *]   $(gen p.gen)
        [%mtrs *]   
      ?.  ?=([%brks *] p.gen)
        [%lmbn %blur]
      gen(p.p (turn p.p.gen =+(*gene |=($$(gen -<)))))
    ::
        *           
      =+  bog=(open gen)
      ?:  .=(bog gen)
        [%lmbn %blur]
      $(gen bog)
    ==
  ==
::
    plan  &[p=*axis q=*(unit &[p=*axis q=*gene]) r=*type]
::::
::
    rake
  ^<  mine.[!=(->) %rake ~]
  =+  gen=*gene
  |=
  ^>  bean.%rake
  ^-  *rope
  ?-  gen
    @             [gen ~]
    [~ *]         [gen ~]
    [%mtbn * ~]   p.gen
    [%htdv *]     $(gen q.gen)
    [%htvd *]     $(gen q.gen)
  ==
::
    ream  ^<(mine.[!=(->) %ream ~] =+(txt=@ |=(^-(*gene [%ream !!]))))
::
    rope  (list <^+(@ *term) [~ p=*axis]>)
::
    spot  &[p=[p=@ q=@] q=[p=@ q=@]]
::::
::
    tool
  |?
    [0 p=@]
    [1 p=*]
    [2 p=*tool q=*tool]
    [3 p=*tool]
    [4 p=*tool]
    [5 p=*tool q=*tool]
    [6 p=*tool q=*tool r=*tool]
    [7 p=*tool q=*tool]
    [8 p=*tool q=*tool]
    [9 p=@ q=*tool]
    [10 p=*<@ [p=@ q=*tool]> q=*tool]
    [p=^+(^ *tool) q=*tool]
  ==
::::
::
    type
  |?
    %atom
    %blur
    %blot
    [%cell p=*type q=*type]
  :: [%core p=*type q=*<| & [| p=*type] [& p=*type]> r=*(book term gene)]
  ::
    :*  %core
        p=*type
        q=*<%blue [%gray p=*type] %pink [%teal p=*type]> 
        r=*(book term gene)
    ==
  ::
    [%cube p=*]
    [%face p=*term q=*type]
    [%fork p=*type q=*type]
    [%hold p=*type q=*gene]
  ==
::::
::
    twin
  ^<  mine.[!=(->) %twin ~]
  =+  [hed=*type tal=*type]
  |=
  ^-  *type
  ?:  .=(%blot hed)
    %blot
  ?:  .=(%blot tal)
    %blot
  [%cell hed tal]
::::
::
    vade
  =+  [tip=*type gen=*gene]
  |=
  (~(mill vane tip) gen)
::::
::
    vane
  ^<    mine.
      :+  !=(->+) 
        %vane 
      :~
        [%mill !=(mill)]
        [%clay !=(clay)]
        [%dump !=(dump)]
      ==
  =+  :*  fan=*(pool &[*type *gene])          ::  20 in van
          ver=*(pool &[*type *type *gene])    ::  42 in van
          bug=@                               ::  86 in van
          vet=?                               ::  87 in van
      ==
  =+  sut=`*type`%blur                        ::  4 in van
  |*
  ::::
  ::
      bake
    ^<  mine.[!=(->) %bake ~]
    =+  [dab=*(book term gene)]
    |=
    ^-  *
    ?-    dab
        ~         0
        [* ~ ~]   (make q.n.dab)
        [* ~ *]   [(make q.n.dab) $(dab r.dab)]
        [* * ~]   [(make q.n.dab) $(dab l.dab)]
        [* * *]   [(make q.n.dab) $(dab l.dab) $(dab r.dab)] 
    ==
  ::::
  ::
      clay
    ^>  bean.%clay
    ?>  ?=([%core *] sut)
    ?-    q.sut
        %blue
      sut
    ::
        [%gray *]
      ?>  ?|(!vet (nest(sut p.q.sut) p.sut))
      [%core p=p.q.sut q=%blue r=r.sut]
    ::
        %pink
      sut
    ::
        [%teal *]
      =+  dox=[%core p.q.sut %pink r.sut]
      ?>  ?|(!vet (fair dox r.sut))
      sut
    ==
  ::::
  ::
      dirt
    ?>  ?=([%core *] sut)
    ?-    q.sut
        %blue       sut
        [%gray *]   [%core p=p.q.sut q=%blue r=r.sut]
        %pink       sut
        [%teal *]   [%core p=p.q.sut q=%pink r=r.sut]
    ==
  ::::
  ::
      dump
    ^-  *ball
    =+  ^=  gil
        =+  den=[p=`@`1 q=*(book type &@)]
        =+  vax=*(pool type)
        =<  q
        |-
        ^-  den
        ?-    sut
            [%cell *]   $(sut q.sut, den $(sut p.sut))
        ::
            [%core *]   
          ?:  ?=(^ q.sut) 
            $(sut p.sut, den $(sut p.q.sut))
          $(sut p.sut)
        ::
            [%face *]   $(sut q.sut)
            [%fork *]   $(sut q.sut, den $(sut p.sut))
        ::
            [%hold *]
          ?:  (~(has in vax) sut)
            [.^(p.den) (~(put by q.den) [sut p.den])]
          $(vax (~(put in vax) sut), sut (play(sut p.sut) q.sut))
        ::
            *           den
        ==
    =+  gax=*(pool type)
    |-
    ?-    sut
        %atom       '@'
        %blot       '#'
        %blur       '*'
    ::
        [%cell *]
      :+  %grip
        ['[' ']']
      :-  $(sut p.sut)
      |-
      ?.  ?=([%cell *] q.sut)
        [$$(sut q.sut) ~]
      [$$(sut p.q.sut) $(q.sut q.q.sut)]
    ::
        [%core *]
      =+  ^=  wut
          %~  dem
            golf
          |-(^-(@ ?~(r.sut 0 .^((add $(r.sut l.r.sut) $(r.sut r.r.sut))))))
      :*  %grip
          ['{' '}']
          :~  %meld
              ?-  q.sut
                  %blue       '+'
                  [%gray *]   '-'
                  %pink       '*'
                  [%teal *]   '/'
              ==
              wut
          ==
          ?-  q.sut
            %blue       [$(sut p.sut) ~]
            [%gray *]   [$(sut p.sut) $(sut p.q.sut) ~]
            %pink       [$(sut p.sut) ~]
            [%teal *]   [$(sut p.sut) $(sut p.q.sut) ~]
          ==
      ==
    ::
        [%cube *]   ?:(.=(0 p.sut) '~' ~(gut golf p.sut))
        [%face *]   [%meld ~(vam golf p.sut) '=' $(sut q.sut) ~]
    ::
        [%fork *]
      ?:  .=([%fork [%cube 0] [%cube 1]] sut)
        '?'
      :+  %grip
        ['<' '>']
      :-  $(sut p.sut)
      |-
      ?.  ?=([%fork *] q.sut)
        [$$(sut q.sut) ~]
      [$$(sut p.q.sut) $(q.sut q.q.sut)]
    ::
        [%hold *]
      =+  piv=(~(get by gil) sut)
      ?~  piv
        $(sut (play(sut p.sut) q.sut))
      :*  %meld
          '%'
          ~(dem golf u.piv)
          ~
          ?:  (~(has in gax) sut)
            ~
          [':' $(gax (~(put in gax) sut), sut (play(sut p.sut) q.sut)) ~]
      ==
    ==
  ::::
  ::
      edit
    ^<  mine.[!=(->) %edit ~]
    =+  [axe=*axis mun=*type har=*(list &[p=*gene q=*gene])]
    |=
    ^>  bean.%mesh
    =+  luf=*(list &[p=*axis q=*tool])
    |-
    ^-  [p=*type q=*tool]
    ?~  har
      [mun (hike axe luf)]
    =+  rep=(rake p.i.har)
    =+  bop=(make(sut mun) p.i.har)
    ?>  ?=([0 *] bop)
    =+  taq=(tuck(sut mun) rep sut q.i.har)
    $(har t.har, mun p.taq, luf [[p.bop q.taq] luf])
  ::::
  ::
      etch
    ^<  mine.[!=(->) %etch ~]
    =+  [rep=*rope gan=*type]
    |=
    =+  per=(flop rep)
    |-
    ^-  *type
    ?~  per
      gan
    ?-    i.per
        @
      =+  wer=(find i.per)
      =+  giv=$(sut r.wer, per t.per)
      |-
      ^-  *type
      ?-    sut
          [%cell *]
        ?<  .=(1 p.wer)
        =+  [tip=(cap p.wer) tap=(mas p.wer)]
        ?:  .=(2 tip)
          [%cell $(sut p.sut, p.wer tap) q.sut]
        ?>  .=(3 tip)
        [%cell p.sut $(sut q.sut, p.wer tap)]
      ::
          [%core *]
        ?:  .=(1 p.wer)
          giv
        =+  [tip=(cap p.wer) tap=(mas p.wer)]
        ?>  .=(2 tip)
        :^    %core
            $(sut p.sut, p.wer tap)
          ?-    q.sut
              %blue       [%gray p.sut]
              [%gray *]   q.sut
              %pink       [%teal p.sut]
              [%teal *]   q.sut
          ==
        r.sut
      ::
          [%face *]
        ?>  ?&(.=(i.per p.sut) .=(1 p.wer))
        [%face p.sut giv]
      ::
          [%fork *]
        (eith $(sut p.sut) $(sut q.sut))
      ::
          [%hold *]
        $(sut (play(sut p.sut) q.sut))
      ==
    ::
        [~ *]
      |-
      ^-  *type
      ?:  .=(1 p.i.per)
        $$(per t.per)
      =+  [tip=(cap p.i.per) tap=(mas p.i.per)]
      ?-    sut
      ::
          %blur       
        $(sut [%cell %blur %blur])
      ::
          [%cell *]   
        ?:  .=(2 tip) 
          [%cell $(sut p.sut, p.i.per tap) q.sut]
        [%cell p.sut $(sut q.sut, p.i.per tap)]
      ::
          [%core *]
        ?>  .=(2 tip) 
        :^    %core
            $(sut p.sut, p.i.per tap)
          ?-    q.sut
              %blue       [%gray p.sut]
              [%gray *]   q.sut
              %pink       [%teal p.sut]
              [%teal *]   q.sut
          ==
        r.sut
      ::
          [%cube *]   
        ?>  .?(p.sut) 
        $(sut [%cell [%cube -.p.sut] [%cube +.p.sut]])
      ::
          [%face *]   [%face p.sut $(sut q.sut)]
      ::
          [%fork *]   (eith $(sut p.sut) $(sut q.sut))
      ::
          [%hold *]   $(sut (play(sut p.sut) q.sut))
      ==
    ==
  ::::
  ::
      fair
    ^<  mine.[!=(->) %fair ~]
    =+  [ref=*type dab=*(book term gene)]
    |=
    ^-  ?
    ^>  bean.%fair
    ?-    dab
        ~         &
        [* ~ ~]   (firm ref q.n.dab)
        [* ~ *]   ?&((firm ref q.n.dab) $(dab r.dab))
        [* * ~]   ?&((firm ref q.n.dab) $(dab l.dab))
        [* * *]   ?&((firm ref q.n.dab) $(dab l.dab) $(dab r.dab))
    ==
  ::::
  ::
      find
    ^<  mine.[!=(->) %find ~]
    =+  cog=*term
    |=
    ^>  bean.%find
    %-  need
    =+  gil=*(pool type)
    |-
    ^-  *(unit plan)
    ?-    sut
        [%cell *]
      =+  taf=$(sut p.sut)
      ?~  taf
        =+  bov=$(sut q.sut)
        ?~  bov
          ~
        [~ (peg 3 p.u.bov) q.u.bov r.u.bov]
      [~ (peg 2 p.u.taf) q.u.taf r.u.taf]
    ::
        [%core *]
      =+  zem=(look cog r.sut)
      ?~  zem
        =+  taf=$(sut p.sut)
        ?~  taf
          ~
        [~ (peg 2 p.u.taf) q.u.taf r.u.taf]
      [~ 1 [~ (peg 3 p.u.zem) q.u.zem] clay]
    ::
        [%face *]
      ?: .=(cog p.sut)
        [~ 1 ~ q.sut]
      ~
    ::
        [%fork *]
      =+  hax=$(sut p.sut)
      =+  yor=$(sut q.sut)
      ?~  hax
        ?~  yor
          ~
        ?>(.=(%blot (peek(sut p.sut) p.u.yor)) yor)
      ?~  yor
        ?>(.=(%blot (peek(sut q.sut) p.u.hax)) hax)
      ?:  .=(hax yor)
        hax
      ?>  ?&(.=(p.u.hax p.u.yor) .=(~ q.u.hax) .=(~ q.u.yor))
      [~ p.u.hax ~ [%fork r.u.hax r.u.yor]]
    ::
        [%hold *]
      ?:  (~(has in gil) sut)
        ~
      =>  .(gil (~(put in gil) sut))
      $(sut (play(sut p.sut) q.sut))
    ::
        *
      ~
    ==
  ::::
  ::
      firm
    ^<  mine.[!=(->) %firm ~]
    =+  [ref=*type gen=*gene]
    |=
    ?:  (~(has in ver) [sut ref gen])
      &
    =>  .(ver (~(put in ver) [sut ref gen]))
    .=((make gen) (make(sut ref) gen))
  ::::
  ::
      fish
    ^<  mine.[!=(->) %fish ~]
    =+  [axe=*axis]
    |=
    =+  vit=*(pool type)
    |-
    ^-  *tool
    ?-  sut
        %atom       (flip [3 0 axe])
        %blot       [1 1]
        %blur       [1 0]
    ::
        [%cell *]
      %+  flan
        [3 0 axe]
      (flan $(sut p.sut, axe (peg axe 2)) $(sut q.sut, axe (peg axe 3)))
    ::
        [%core *]   [0 0]
        [%cube *]   [5 [1 p.sut] [0 axe]]
        [%face *]   $(sut q.sut)
    ::
        [%fork *]
      (flor $(sut p.sut) $(sut q.sut))
    ::
        [%hold *]
      ?<  (~(has in vit) sut)
      =>  %=(. vit (~(put in vit) sut))
      $(sut (play(sut p.sut) q.sut))
    ==
  ::::
  ::
      gain
    ^<  mine.[!=(->) %gain ~]
    =+  [gen=*gene]
    |=
    ^-  *type
    ?-    gen
        [%htdv *]   $(gen q.gen)
        [%htvd *]   $(gen q.gen)
        [%ksbn *]   (etch (rake q.gen) (reap(sut (play q.gen)) (play p.gen)))
        [%kspm *]   |-(?~(p.gen sut $(p.gen t.p.gen, sut $$(gen i.p.gen))))
        *           sut
    ==
  ::
      make  ^<(mine.[!=(->) %make ~] =+(gen=*gene |=(q:(mill gen))))
  ::::
  ::
      mesh
    ^<  mine.[!=(->) %mesh ~]
    =+  [axe=*axis mun=*type har=*(list &[p=*gene q=*gene])]
    |=
    ^>  bean.%mesh
    =+  luf=*(list &[p=*axis q=*tool])
    |-
    ^-  [p=*type q=*tool]
    ?~  har
      [mun (hike axe luf)]
    =+  rep=(rake p.i.har)
    =+  cux=(mill q.i.har)
    =+  bop=(make(sut mun) p.i.har)
    ?>  ?=([0 *] bop)
    $(har t.har, mun (etch(sut mun) rep p.cux), luf [[p.bop q.cux] luf])
  ::::
  ::
      mill
    ^<  mine.[!=(->) %mill ~]
    =+  [gen=*gene]
    |=
    ^-  [p=*type q=*tool]
    ?-    -.gen
    ::
        %bnvd                                                           ::  =>
      =+  fid=$(gen p.gen)
      =+  dov=$(sut p.fid, gen q.gen)
      [p.dov (comb q.fid q.dov)]
    ::
        %brmt                                                           ::  |%
      =+  sac=(~(gas by ~) p.gen) 
      =+  toc=[%core sut %pink sac]
      [toc [[0 1] [1 (bake(sut toc) sac)]]]
    ::
        %brrs                                                           ::  |*
      =+  sac=(~(gas by ~) p.gen) 
      =+  toc=[%core sut %blue sac]
      [toc [[0 1] [1 (bake(sut toc) sac)]]]
    ::
        %dgnp                                                           ::  :-
      =+  hed=$(gen p.gen)
      =+  tal=$(gen q.gen)
      [[%cell p.hed p.tal] (cons q.hed q.tal)]
    ::
        %dtbn                                                           ::  .=
      :-  [%fork [%cube 0] [%cube 1]] 
      [5 q:$(gen p.gen) q:$(gen q.gen)]
    ::
        %dtht                                                           ::  .^
      =+  sam=$(gen p.gen)
      ?>  ?|(!vet (nest(sut %atom) p.sam))
      [%atom [4 q.sam]]
    ::
        %dtrs                                                           ::  .*
      [%blur [2 q:$(gen p.gen) q:$(gen q.gen)]]
    ::
        %dtsg                                                           ::  .~ 
      [[%cube p.gen] [1 p.gen]]
    :: 
        %dtks                                                           ::  .?
      [[%fork [%cube 0] [%cube 1]] [3 (make p.gen)]]
    ::
        %hpbn                                                           ::  !=
      =+  sev=q:$(gen p.gen)
      [[%cube sev] [1 sev]]
    ::
        %hpdg 
      =+  vot=(play p.gen)
      [[%cube vot] [1 vot]]
    ::
        %hphp                                                           ::  !!
      [%blot [0 0]]
    ::
        %hpmt                                                           ::  !%
      [%blur [1 ..vane]]          ::  needs !: for bootstrap
    ::
        %htbn                                                           ::  ^=
      =+  vat=$(gen q.gen)
      [(name(sut p.vat) p.gen) q.vat]
    ::
        %htnp                                                           ::  ^-
      =+  hif=(play p.gen)
      =+  dan=$(gen q.gen)
      ^>  bean.[%htnp-hif |=((~(fly club dump(sut hif)) 75))]
      ^>  bean.[%htnp-dan |=((~(fly club dump(sut p.dan)) 75))]
      ?>  ?|(!vet (nest(sut hif) p.dan))
      [hif q.dan]
    ::
        %htpd                                                           ::  ^+
      ?>  ?|(!vet (nest(sut (play p.gen)) (play q.gen)))
      [sut [0 1]]
    ::
        %htsg                                                           ::  ^~
      $(gen p.gen)
    ::
        %htvd                                                           ::  ^>
      =<  dext
      |*
      ::::
      ::
          dext
        ?.  ?=([%spot %dtsg *] p.gen)
          sint
        ^>  bean.[%spot |=((~(fly club ~(loc golf +>.p.gen)) 75))]
        sint
      ::::
      ::
          sint
        =+  hum=$(gen q.gen)
        :-  p.hum
        :+  10
          ?-    p.gen
              @   p.gen
              ^   [p.p.gen (make q.p.gen)]
          ==
        q.hum
      ::
      ::::
      ==
    ::
        %ksbn                                                           ::  ?=
      =+  vol=(make q.gen)
      =+  wam=(play p.gen)
      ?>  ?=([0 *] vol)
      :-  [%fork [%cube 0] [%cube 1]]
      (fish(sut wam) p.vol)
    ::
        %ksdg                                                           ::  ?:
      =+  nor=$(gen p.gen)
      =+  fex=(gain p.gen)
      =+  hiq=$(sut fex, gen q.gen)
      =+  ran=$(gen r.gen)
      ?>  ?|(!vet (nest(sut [%fork [%cube 0] [%cube 1]]) p.nor))
      :-  (eith p.hiq p.ran)
      [6 q.nor q.hiq q.ran]
    :: 
        %mtbn                                                           ::  %=
      =+  lar=(seek p.gen)
      =+  fup=(mesh p.lar r.lar q.gen)
      ?~  q.lar
        fup
      :-  [%hold clay(sut p.fup) q.u.q.lar] 
      [9 p.u.q.lar q.fup]
    ::
        *       
      $(gen (open gen))
    ==
  ::::
  ::
      name
    ^<  mine.[!=(->) %name ~]
    =+  cog=*term
    |=
    ^-  *type
    ?:  .=(%blot sut)
      %blot
    [%face cog sut]
  ::::
  ::
      nest
    ^<  mine.[!=(->) %nest ~]
    =+  bon=*type
    |=
    ^>  bean.%nest
    ^-  ?
    =+  gil=*(pool &[p=*type q=*type])
    =<  dext
    |*
    ::::
    ::
        dext
      ^-  ?
      ?:  .=(sut bon)
        &
      ?-  sut
          %atom       ?-(bon %atom &, [%cube *] !.?(p.bon), * sint)
      ::
          %blot       sint
      ::
          %blur       &
      ::
          [%cell *]
        ?.  ?=([%cell *] bon)
          sint
        ?&
          dext(sut p.sut, bon p.bon)
          dext(sut q.sut, bon q.bon)
        ==
      ::
          [%core *]   
        ?.  ?=([%core *] bon) 
          sint 
        ?&
          dext(sut p.sut, bon p.bon)
          =>(.(sut dirt, bon dirt(sut bon)) .=(+>.sut +>.bon))
        ==
      ::
          [%cube *]   ?.(?=([%cube *] bon) sint .=(sut bon))
      ::
          [%face *]   dext(sut q.sut)
      ::
          [%fork *]
        ?.  ?=(*<%atom %blur [%cell *] [%cube *] [%core *]> bon)
          sint
        ?|(dext(sut p.sut) dext(sut q.sut))
      ::
          [%hold *]
        ?|
          (~(has in gil) [sut bon])
          dext(gil (~(put in gil) [sut bon]), sut (play(sut p.sut) q.sut))
        ==
      ==
    ::::
    ::
        sint
      ^-  ?
      ?-  bon
          %atom       |
      ::
          %blot       &
      ::
          %blur       |
      ::
          [%cell *]   |
      ::
          [%core *]   dext(bon [%cell p.bon %blur])
      ::
          [%cube *]               
        ?&
          .?(p.bon) 
          dext(bon [%cell [%cube -.p.bon] [%cube +.p.bon]])
        ==
      ::
          [%face *]   dext(bon q.bon)
      ::
          [%fork *]   ?&(dext(bon p.bon) dext(bon q.bon))
      ::
          [%hold *]
        ?|
          (~(has in gil) [sut bon])
          dext(gil (~(put in gil) [sut bon]), bon (play(sut p.bon) q.bon))
        ==
      ==
    ::
    ::::
    ==
  ::::
  ::
      orth
    =+  ref=*type
    |=
    .=(%blot (reap ref))
  ::::
  ::
      peek
    ^<  mine.[!=(->) %peek ~]
    =+  axe=*axis
    |=
    ^-  *type
    ?:  .=(1 axe)
      sut
    =+  [tip=(cap axe) tap=(mas axe)]
    ?-    sut
        %atom       %blot
        %blot       %blot
        %blur       %blur
        [%cell *]   ?:(.=(2 tip) $(sut p.sut, axe tap) $(sut q.sut, axe tap))
        [%core *]   ?:(.=(2 tip) $(sut p.sut, axe tap) %blur)
    ::
        [%cube *]   
      ?.  .?(p.sut) 
        %blot 
      ?:  .=(2 tip) 
        $(sut [%cube -.p.sut], axe tap)
      $(sut [%cube +.p.sut], axe tap)
    ::
        [%face *]   $(sut q.sut)
        [%fork *]   (eith $(sut p.sut) $(sut q.sut))
        [%hold *]   $(sut (play(sut p.sut) q.sut))
    ==
  ::
      play  =+(gen=*gene |=(p:(mill(vet |) gen)))
  ::
      read  ^<(mine.[!=(->) %read ~] =+(txt=*tape |=(^-(*gene [%read !!]))))
  ::::
  ::
      reap
    ^<  mine.[!=(->) %reap ~]
    =+  ref=*type
    |=
    =+  bol=*(pool type)
    =<  dext
    |*
    ::::
    ::
        dext
      ^-  *type
      ?-    sut
      ::
          %atom
        ?-    ref
            %atom       %atom
            [%cube *]   ?:(.?(p.ref) %blot ref)
            *           sint
        ==
      ::
          %blot
        %blot
      ::
          %blur
        ref
      ::
          [%cell *]
        ?-    ref
        ::
            [%cube *]   
          ?.  .?(p.ref) 
            %blot
          %+  twin
            dext(sut p.sut, ref [%cube -.p.ref]) 
          dext(sut q.sut, ref [%cube +.p.ref])
        ::
            [%cell *]
          (twin dext(sut p.sut, ref p.ref) dext(sut q.sut, ref q.ref))
        ::
            *
          sint
        ==
      ::
          [%core *]
        sut
      ::
          [%cube *]
        ?-    ref
        ::
            %atom
          ?:(.?(p.sut) %blot sut)
        ::
            [%cell *]
          ?.  .?(p.sut) 
            %blot 
          %+  twin
            dext(sut [%cube -.p.sut], ref p.ref) 
          dext(sut [%cube +.p.sut], ref q.ref)
        ::
            [%cube *]
          ?:(.=(p.sut p.ref) sut %blot)
        ::
            *
          sint
        ==
      ::
          [%face *]
        (name(sut dext(sut q.sut)) p.sut) 
      ::
          [%fork *]   
        (eith dext(sut p.sut) dext(sut q.sut))
      ::
          [%hold *]   
        =+  yet=(play(sut p.sut) q.sut)
        =+  woz=dext(sut yet)
        ?:  .=(woz yet)
          sut
        woz
      ==
    ::::
    ::
        sint
      ^-  *type
      ?-    ref
          %blur       sut
          [%core *]   dext(ref [%cell p.ref %blur])
          [%face *]   dext(ref q.ref)
          [%fork *]   (eith dext(ref p.ref) dext(ref q.ref))
      ::
          [%hold *]
        ?<  (~(has in bol) ref)
        dext(bol (~(put in bol) ref), ref (play(sut p.ref) q.ref))
      ::
          *
        %blot
      ==
    ::
    ::::
    ==
  ::::
  ::
      seek
    ^<  mine.[!=(->) %seek ~]
    =+  rep=*rope
    |=
    ^>  bean.%seek
    ^-  *plan
    ?~  rep
      [1 ~ sut]
    =+  vag=$(rep t.rep)
    ?-    i.rep
        @
      =+  hup=(find(sut r.vag) i.rep)
      [(peg p.vag p.hup) q.hup r.hup]
    ::
        [~ *]
      [(peg p.vag p.i.rep) ~ (peek(sut r.vag) p.i.rep)]
    ==
  ::::
  ::
      snap
    ^<  mine.[!=(->) %snap ~]
    =+  [ref=*type gen=*gene]
    |=
    ^-  *gene 
    ?-    sut
        [%cell *]
      |-
      ?-    gen
          [%dgnp *]   [%dgnp $$(sut p.sut, gen p.gen) $$(sut q.sut, gen q.gen)]
          [%hpcb *]   [%hpcb p.gen $(gen q.gen)]
          *
        =+  voq=(open gen)
        ?:  .=(gen voq)
          gen
        =+  hal=$(gen voq)
        ?:  .=(hal voq)
          gen
        hal
      ==
    ::
        [%face *]   [%htbn p.sut $(sut q.sut)]
        [%fork *]
      =+  vap=(play(sut ref) gen)
      ?:  (orth(sut p.sut) vap)
        ?:  (orth(sut q.sut) vap)
          gen
        $(sut q.sut) 
      ?:  (orth(sut q.sut) vap)
        $(sut p.sut)
      gen
    ::
        *           gen
    ==
  ::::
  ::
      tuck
    ^<  mine.[!=(->) %tuck ~]
    =+  [rep=*rope boz=*type rix=*gene]
    |=
    =+  per=(flop rep)
    |-
    ^-  [p=*type q=*tool]
    ?~  per
      (mill(sut boz) rix)
    ?-    i.per
        @
      =+  wer=(find i.per)
      =+  giv=$(sut r.wer, per t.per)
      :-  |-
          ^-  *type
          ?-    sut
              [%cell *]
            ?<  .=(1 p.wer)
            =+  [tip=(cap p.wer) tap=(mas p.wer)]
            ?:  .=(2 tip)
              [%cell $(sut p.sut, p.wer tap) q.sut]
            ?>  .=(3 tip)
            [%cell p.sut $(sut q.sut, p.wer tap)]
          ::
              [%core *]
            ?:  .=(1 p.wer)
              p.giv
            =+  [tip=(cap p.wer) tap=(mas p.wer)]
            ?>  .=(2 tip)
            :^    %core
                $(sut p.sut, p.wer tap)
              ?-    q.sut
                  %blue       [%gray p.sut]
                  [%gray *]   q.sut
                  %pink       [%teal p.sut]
                  [%teal *]   q.sut
              ==
            r.sut
          ::
              [%face *]
            ?>  ?&(.=(i.per p.sut) .=(1 p.wer))
            [%face p.sut p.giv]
          ::
              [%fork *]
            (eith $(sut p.sut) $(sut q.sut))
          ::
              [%hold *]
            $(sut (play(sut p.sut) q.sut))
          ==
      q.giv
    ::
        [~ *]
      ^-  [p=*type q=*tool]
      =-  ?~  t.per
            $(per ~, rix (snap rix))
          $(per t.per)
      %=    .
          sut
        |-
        ^-  *type
        ?:  .=(1 p.i.per)
          sut
        =+  [tip=(cap p.i.per) tap=(mas p.i.per)]
        ?-    sut
        ::
            %blur       
          $(sut [%cell %blur %blur])
        ::
            [%cell *]   
          ?:  .=(2 tip) 
            [%cell $(sut p.sut, p.i.per tap) q.sut]
          [%cell p.sut $(sut q.sut, p.i.per tap)]
        ::
            [%core *]
          ?>  .=(2 tip) 
          :^    %core
              $(sut p.sut, p.i.per tap)
            ?-    q.sut
                %blue       [%gray p.sut]
                [%gray *]   q.sut
                %pink       [%teal p.sut]
                [%teal *]   q.sut
            ==
          r.sut
        ::
            [%cube *]   
          ?>  .?(p.sut) 
          $(sut [%cell [%cube -.p.sut] [%cube +.p.sut]])
        ::
            [%face *]   $(sut q.sut)
        ::
            [%fork *]   (eith $(sut p.sut) $(sut q.sut))
        ::
            [%hold *]   $(sut (play(sut p.sut) q.sut))
        ==
      ==
    ==
  ::
  ::::
  ==
==
