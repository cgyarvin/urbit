^<  mine.
  :+  ~
    [%watt 271]
  :~
    [%ut !=(ut)]
    [%to !=(to)]
  --
|*
  ::  Tier 1, light math
::::
::
    add
  ^<  mine.[!=(->) %add ~]
  =+  [a=@ b=@]
  |=
  ^-  @
  ?:  .=(0 a)
    b
  $(a (dec a), b .^(b))
::::
::
    bex
  ^<  mine.[!=(->) %bex ~] 
  =+  [a=@]
  |=
  ^-  @
  ?:  .=(0 a)
    1
  (mul 2 $(a (dec a)))
::::
::
    dec
  ^<  mine.[!=(->) %dec ~] 
  =+  [a=@]
  |=
  ^-  @
  ^>  bean.%dec
  ?<  .=(0 a)
  =+  b=@
  |-
  ?:  .=(a .^(b))
    b
  $(b .^(b))
::::
::
    div
  ^<  mine.[!=(->) %div ~] 
  =+  [a=@ b=@]
  |=
  ^-  @
  ^>  bean.%div
  ?<  .=(0 b)
  =+  c=@
  |-
  ?:  (lth a b)
    c
  $(a (sub a b), c .^(c))
::::
::
    fib
  =+  a=@
  |=
  ^>  memo
  ?-  a
    0   0
    1   1
    *   (add $(a (sub a 1)) $(a (sub a 2)))
  --
::::
::
    gte
  ^<  mine.[!=(->) %gte ~] 
  =+  [a=@ b=@]
  |=
  ^-  ?
  !(lth a b)
::::
::
    gth
  ^<  mine.[!=(->) %gth ~] 
  =+  [a=@ b=@]
  |=
  ^-  ?
  !(lte a b)
::::
::
    lte
  ^<  mine.[!=(->) %lte ~] 
  =+  [a=@ b=@]
  |=
  ?|(.=(a b) (lth a b))
::::
::
    lth
  ^<  mine.[!=(->) %lth ~] 
  =+  [a=@ b=@]
  |=
  ^-  ?
  ?&
    !.=(a b)
  ::
    |-
    ?:  .=(0 a)
      &
    ?:  .=(0 b)
      |
    $(a (dec a), b (dec b))
  --
::::
::
    mod
  ^<  mine.[!=(->) %mod ~] 
  =+  [a=@ b=@]
  |=
  ^-  @
  ?<  .=(0 b)
  (sub a (mul b (div a b)))
::::
::
    mul
  ^<  mine.[!=(->) %mul ~] 
  =+  [a=@ b=@]
  |=
  ^-  @
  =+  c=^-(@ 0)
  |-
  ?:  .=(0 a)
    c
  $(a (dec a), c (add b c))
::::
::
    nop
  =+  *
  |:
  -<
::::
::
    sub
  ^<  mine.[!=(->) %sub ~] 
  =+  [a=@ b=@]
  |=
  ^-  @
  ?:  .=(0 b)
    a
  $(a (dec a), b (dec b))
::
::::  
  ::  Tier 2, lists and units
::::
::
    bind
  =+  [*(unit) &*]
  |:
  =>  .(-< [a=-<- b=-<+])
  ?~  a
    ~
  [~ u=(b u.a)]
::::
::
    clap
  =+  [a=*(unit) b=*(unit) c==+(^ |=(-<-))]
  |:
  ?~  a
    b
  ?~  b
    a
  [~ u=(c u.a u.b)]
::::
::
    drop
  =+  a=*(unit)
  |:
  ?~  a
    ~
  [i=u.a t=~]
::::
::
    flop
  =+  a=*(list)
  |:
  ^-  a
  =+  b=`a`~
  |-
  ?~  a
    b
  $(a t.a, b [i.a b])
::::
::
    lent
  =+  a=*(list)
  |=
  ^-  @
  =+  b=@
  |-
  ?~(a b $(a t.a, b .^(b)))
::::
::
    levy
  =+  [a=*(list) b==+(* |=(.?(-<)))]
  |:  |-
  ^-  ?
  ?~  a
    &
  ?:  (b i.a)
    $(a t.a)
  |
::::
::
    lien
  =+  [a=*(list) b==+(* |=(.?(-<)))]
  |:  |-
  ^-  ?
  ?~  a
    |
  ?:  (b i.a)
    &
  $(a t.a)
::::
::
    list
  =+  a=&*
  |:
  =+  b=*
  |=
  ?-  b
    ~           ~
    [i=* t=*]   [i=(a i.b) t=$(b t.b)]
  --
::::
::
    need
  =+  a=*(unit)
  |:
  ?~  a
    !!
  u.a
::::
::
    reel
  =+  [a=*(list) b==+([* *] |=(-<+))]
  |:  |-
  ^-  -<+.b
  ?~  a
    -<+.b
  $:b(-<- i.a, -<+ $(a t.a))
::::
::
    roll
  =+  [a=*(list) b==+([* *] |=(-<+))]
  |:  |-
  ^-  -<+.b
  ?~  a
    -<+.b
  $(a t.a, b b(-<+ $:b(-<- i.a)))
::::
::
    skim
  =+  [a=*(list) b==+([p=*] |=(.?(p)))]
  |:  |-
  ^-  a
  ?~  a
    ~
  ?:((b i.a) [i.a $(a t.a)] $(a t.a))
::::
::
    skip
  =+  [a=*(list) b==+([p=*] |=(.?(p)))]
  |:  |-
  ^-  a
  ?~  a
    ~
  ?:((b i.a) $(a t.a) [i.a $(a t.a)])
::::
::
    snag
  =+  [a=@ b=*(list)]
  |:  |-
  ?~  b
    !!
  ^-  i.b
  ?:  .=(0 a)
    i.b
  $(b t.b, a (dec a))
::::
::
    sort
  =+  [a=*(list) b==+([p=* q=*] |=(.=(p q)))]
  |:  |-
  ^-  a
  ?~  a
    ~
  %+  weld
    $(a (skim ^@(a) =+(* |:((b -< i.a)))))
  [i.a $(a (skim ^@(a) =+(* |:((b i.a -<)))))]
::::
::
    tree
  =+  a=&*
  |:
  =+  b=*
  |=
  ?-  b
    ~               ~
    [n=* l=* r=*]   [n=(a n.b) l=$(b l.b) r=$(b r.b)]
  --
::::
::
    turn
  =+  [a=*(list) b=&*]
  |:  |-
  ?~  a
    ~
  [i=(b i.a) t=$(a t.a)]
::::
::
    unit
  =+  a=&*
  |:
  =+  b=*
  |=
  ?-  b
    ~       ~
    [~ *]   [~ u=(a +.b)]
  --
::::
::
    weld
  =+  [a=*(list) b=*(list)]
  |:  |-
  ^-  b
  ?~  a
    b
  [i.a $(a t.a)]
::
::::
  ::  Tier 3, noun surgery
::::
::
    axis  &@
    bloq  &@
::::
::
    cap
  ^<  mine.[!=(->) %cap ~] 
  =+  [a=*axis]
  |=
  ^-  *<2 3>
  ?-  a
    2       2
    3       3
    *<0 1>  !!
    *       $(a (div a 2))
  --
::::
::  
    cat
  ^<  mine.[!=(->) %cat ~] 
  =+  [a=*bloq b=@ c=@]
  |=
  (add (lsh a (met a b) c) b)
::::
::
    con
  ^<  mine.[!=(->) %con ~]
  =+  [a=@ b=@]
  |=
  =+  [c=`@`0 d=`@`0]
  |-
  ?:  ?&(.=(0 a) .=(0 b))
    d
  %=  $
    a   (rsh 0 1 a)
    b   (rsh 0 1 b)
    c   .^(c)
    d   (add d (lsh 0 c ?&(.=(0 (end 0 1 a)) .=(0 (end 0 1 b)))))
  --
::::
::
    cut
  ^<  mine.[!=(->) %cut ~]
  =+  [a=*bloq [b=@ c=@] d=@]
  |=
  (end a c (rsh a b d))
::::
::
    dis
  ^<  mine.[!=(->) %dis ~]
  =+  [a=@ b=@]
  |=
  =+  [c=`@`0 d=`@`0]
  |-
  ?:  ?|(.=(0 a) .=(0 b))
    d
  %=  $
    a   (rsh 0 1 a)
    b   (rsh 0 1 b)
    c   .^(c)
    d   (add d (lsh 0 c ?|(.=(0 (end 0 1 a)) .=(0 (end 0 1 b)))))
  --
::::
::
    dor
  ^<  mine.[!=(->) %dor ~]
  =+  [a=* b=*]
  |=
  ^-  ?
  ?:  .=(a b)
    &
  ?.  ?=(@ a)
    ?.  ?=(@ b)
      ?:  .=(-.a -.b)
        $(a +.a, b +.b)
      $(a -.a, b -.a)
    |
  ?.  ?=(@ b)
    &
  (lth a b)
::::
::
    end 
  ^<  mine.[!=(->) %end ~]
  =+  [a=*bloq b=@ c=@]
  |=
  (mod c (bex (mul (bex a) b)))
::::
::
    gor
  ^<  mine.[!=(->) %gor ~]
  =+  [a=* b=*]
  |=
  ^-  ?
  =+  [c=(mug a) d=(mug b)]
  ?:  .=(c d)
    (dor a b)
  (lth c d)
::::
::
    lsh
  ^<  mine.[!=(->) %lsh ~]
  =+  [a=*bloq b=@ c=@]
  |=
  (mul (bex (mul (bex a) b)) c)
::::
::
    mix
  ^<  mine.[!=(->) %mix ~]
  =+  [a=@ b=@]
  |=
  =+  [c=`@`0 d=`@`0]
  |-
  ?:  ?&(.=(0 a) .=(0 b))
    d
  %=  $
    a   (rsh 0 1 a)
    b   (rsh 0 1 b)
    c   .^(c)
    d   (add d (lsh 0 c .=((end 0 1 a) (end 0 1 b))))
  --
::::
::
    met
  ^<  mine.[!=(->) %met ~]
  =+  [a=*bloq b=@]
  |=
  =+  c=`@`0
  |-
  ?:  .=(0 b)
    c
  $(b (rsh a 1 b), c .^(c))
::::
::
    mas
  ^<  mine.[!=(->) %mas ~]
  =+  [a=*axis]
  |=
  ^-  *axis
  ?-  a
    1   !!
    2   1
    3   1
    *   (add (mod a 2) (mul $(a (div a 2)) 2))
  --
::::
::
    mug
  ^<  mine.[!=(->) %mug ~]
  =+  a=*
  |=
  ^-  @
  =<  $
  |*
  ::::
  ::
      $
    ?.  ?=(@ a)
      =+  [b=$(a -.a) c=$(a +.a)]
      =+  d=(mix b (add (end 5 1 (lsh 3 1 c)) (rsh 3 3 c)))
      |-
      =+  e=(dis 0x7fffffff (chum d))
      ?:  .=(0 e)
        $(d .^(d))
      e
    =+  b=`@`0x18d0a625
    |-
    =+  c=(met 5 a)
    =+  d=`@`0
    =+  e=b
    |-
    ?:  .=(d c)
      =+  f=(dis 0x7fffffff e)
      ?:  .=(0 f)
        $$(b .^(b))
      f
    $(d .^(d), e (chum (mix e (cut 5 [1 d] a))))
  ::::
  ::
      chum
    =+  [b=@]
    |=
    =-  (end 5 1 -)
    (dice 3 (dice 2 (dice 1 (dice 0 b))))
  ::::
  ::
      dice
    =+  [b=@ c=@]
    |=
    (mix c (sbox (cut 3 [1 b] c))) 
  ::::
  ::
      sbox
    =+  [b=@]
    |=
    =-  (cut 5 [1 b] -)
    0x45bd2fb7\ /eec357b8\ /2ae29f22\ /bb412e03\ /08445336\ /52ca46d8\
     /76d680b9\ /b9e06f14\ /6f8b9c36\ /a53ef834\ /e3533b5b\ /c5cd423e\
     /0cfd4d14\ /12924b1a\ /3e77ee18\ /1804e2a5\ /75d9c4e1\ /6d4b9a3e\
     /5d56c751\ /12ad4163\ /b9df9e63\ /fbfbdac7\ /54464f51\ /4b72be7f\
     /180d4405\ /1b456773\ /5fb51d73\ /2d4774c2\ /381585e2\ /0209e49e\
     /52c0256a\ /6deac45d\ /3f55045c\ /40b92e52\ /5242aa7a\ /a47337b1\
     /bc816aba\ /4bbb0ecf\ /f82cb4ad\ /0bc99c47\ /78a5c74c\ /9182f20d\
     /7b941dc3\ /ff307dd2\ /fc36ca9d\ /0c32efad\ /b1c38ca7\ /b799e1be\
     /fd0dbfee\ /9887957d\ /70186f9e\ /31325eca\ /48f0ac44\ /c7eb6480\
     /ab1f9401\ /167cf21d\ /dce5b8fa\ /121c9f64\ /e95eacb5\ /c50dca03\
     /8b29fd75\ /875f1bb9\ /261c8938\ /3bf794dc\ /e4395998\ /1b06df20\
     /19c11553\ /eefbdb16\ /54447c57\ /f54a4761\ /bdf13b8c\ /88502921\
     /72553aee\ /c63a1157\ /03a1f284\ /f5b59c3c\ /96923c87\ /e2c0936d\
     /4f112d16\ /0f4c5845\ /1055a07d\ /d82afef8\ /72fb1b49\ /0fa54e14\
     /637d01f5\ /7fe89a8b\ /0617eda8\ /4354f3e7\ /712f6316\ /9036b901\
     /111a1094\ /70c78bcc\ /28329ab3\ /fdf004ca\ /2026a870\ /85ac98d2\
     /3f52439e\ /e030c8ed\ /a7589924\ /05af69ed\ /566dd09c\ /4237062e\
     /e9fc60eb\ /c415321c\ /ac4fc47f\ /b265b0ee\ /95cdc493\ /3beb6b4d\
     /e777884a\ /370f9e65\ /6a52c674\ /62dc97c6\ /e5d5238e\ /b6315b6a\
     /c845a9e2\ /026c78ab\ /13beec00\ /b3ed287a\ /a01ebf2b\ /b5c0da93\
     /deeedce8\ /3e5ec58d\ /e83350f0\ /e6ffe2fb\ /a3765510\ /37f42e48\
     /474c90cf\ /78eea53d\ /d10faca2\ /a9417956\ /7c600063\ /14187d04\
     /faffdd23\ /44844992\ /61f4a52d\ /a234566b\ /a46b9dd7\ /1a9d0bd3\
     /cf3e4033\ /b3bcada3\ /00f3ef4e\ /7367a5f9\ /5e24070c\ /1b973827\
     /5dcd3652\ /95b1eb1f\ /035857a4\ /c090c33b\ /5eba8280\ /475d8534\
     /7fb44be7\ /3bffc889\ /dbaf1fa2\ /948328b1\ /56a8a01c\ /6717fa39\
     /4c41d2c6\ /b3275702\ /51634e92\ /51a573dd\ /d3ea1b9d\ /be74ea0b\
     /9093fa5f\ /d2e1d0d1\ /8a1fd93a\ /8f1d0a6e\ /b7a9e735\ /deb0aa12\
     /48d76271\ /e808ec02\ /8e498a90\ /83539726\ /86c7973e\ /44337e55\
     /81f4f7ca\ /3052b642\ /873b540e\ /68d8efa2\ /18494012\ /decc1486\
     /29079afa\ /b81a7b1d\ /63dd42a0\ /3cab1b6f\ /713fffa2\ /08835b42\
     /6dd6f5f8\ /65a14a04\ /5eab8eac\ /2fe5b7f9\ /bbc96404\ /f593e52b\
     /b2078d1a\ /f192cbcd\ /c5e102b2\ /9187837c\ /bddd8a18\ /5caf8a56\
     /3b7aa391\ /88fd1802\ /1e458e1d\ /5f606388\ /513ce98d\ /aed1093b\
     /ae87124c\ /f537ea14\ /0f4bb673\ /cfbeae41\ /123e5e9f\ /e14077a2\
     /40acd8dc\ /103b81cf\ /683fa466\ /9b9e2cec\ /23171ec6\ /2ba16060\
     /d3bfabae\ /dd68fcd2\ /f60ee7be\ /f44c697e\ /7990bd4e\ /3ce83919\
     /bc457a9d\ /5022a039\ /a158b454\ /80722ba6\ /87652d85\ /e2ddee5a\
     /5c43205e\ /c62f6816\ /4c680e34\ /151f4b17\ /9570fa83\ /eb9f040e\
     /22b9bd9c\ /d156e14b\ /3a30bdd8\ /f00fc9e2\ /62bcfdef\ /da4952df\
     /0c100eda\ /ccffce0a\ /1994625d\ /b0cd88c6\ /766ff6ec\ /7c8f831a\
     /7824fff6\ /dee5b579\ /99267e88\ /f8820121\ /6220ecc5\ /f40f7a8d\
     /fe8c511a\ /a9afcd63\ /ba9cd382\ /352598d8
  ::
  ::::
  ==
::::
::
    peg
  ^<  mine.[!=(->) %peg ~]
  =+  [a=*axis b=*axis]
  |=
  ^-  *axis
  ?-  b
    1   a
    2   (mul a 2)
    3   .^((mul a 2))
    *   (add (mod b 2) (mul $(b (div b 2)) 2))
  --
::::
::
    rap
  ^<  mine.[!=(->) %rap ~]
  =+  [a=*bloq b=*(list &@)]
  |=
  ^-  @
  ?~  b
    0
  (cat a i.b $(b t.b))
::::
::
    rip
  ^<  mine.[!=(->) %rip ~]
  =+  [a=*bloq b=@]
  |=
  ^-  *(list &@)
  ?:  .=(0 b)
    ~
  [(end a 1 b) $(b (rsh a 1 b))]
::::
::
    rsh
  ^<  mine.[!=(->) %rsh ~]
  =+  [a=*bloq b=@ c=@]
  |=
  (div c (bex (mul (bex a) b)))
::::
::
    vor
  ^<  mine.[!=(->) %vor ~]
  =+  [a=* b=*]
  |=
  ^-  ?
  =+  [c=(mug (mug a)) d=(mug (mug b))]
  ?:  .=(c d)
    (dor a b)
  (lth c d)
::
::::
  ::  Tier 4, associative containers 
::::
::
    pool  =+(a=&* |:((tree a)))
    book  =+([a=&* b=&*] |:((tree &[p=*a q=*b])))
::::
::
    in
  ^<  mine.[!=(->) %in [[%tap !=(tap)] ~]]
  =+  a=*(pool)
  |%
  ::::
  ::
      all
    =+  b==+(* |=(?))
    |:  |-
    ^-  ?
    ?~  a
      &
    ?&((b n.a) $(a l.a) $(a r.a))
  ::::
  ::
      any
    =+  b==+(* |=(?))
    |:  |-
    ^-  ?
    ?~  a
      |
    ?|((b n.a) $(a l.a) $(a r.a))
  ::::
  ::
      gas
    =+  b=*(list)
    |:  |-
    ^-  a
    ?~  b
      a
    $(b t.b, a (put i.b))
  ::::
  ::
      has
    ^<  mine.[!=(->) %has ~]
    =+  b=*
    |:  |-
    ^-  ?
    ?~  a
      |
    ?:  .=(b n.a)
      &
    ?:  (gor b n.a)
      $(a l.a)
    $(a r.a)
  ::::
  ::
      put
    ^<  mine.[!=(->) %put ~]
    =+  b=*
    |:  |-
    ^-  a
    ?~  a
      [b ~ ~]
    ?:  .=(b n.a)
      a
    ?:  (gor b n.a)
      =+  c=$(a l.a)
      ?>  ?=(^ c)
      ?:  (vor n.a n.c)
        [n.a c r.a]
      [n.c l.c [n.a r.c r.a]]
    =+  c=$(a r.a)
    ?>  ?=(^ c)
    ?:  (vor n.a n.c)
      [n.a l.a c] 
    [n.c [n.a l.a l.c] r.c]
  ::::
  ::
      tap
    =+  b=*(list)
    |:  |-
    ^-  b
    ?~  a
      b
    $(a r.a, b [n.a $(a l.a)])
  ::
  ::::
  ==
::::
::
    by
  ^<  mine.[!=(->) %by ~]
  =+  a=*(book)
  |%
  ::::
  ::
      all
    =+  b==+(* |=(?))
    |:  |-
    ^-  ?
    ?~  a
      &
    ?&((b q.n.a) $(a l.a) $(a r.a))
  ::::
  ::
      any
    =+  b==+(* |=(?))
    |:  |-
    ^-  ?
    ?~  a
      |
    ?|((b q.n.a) $(a l.a) $(a r.a))
  ::::
  ::
      gas
    ^<  mine.[!=(->) %gas ~]
    =+  b=*(list &[p=* q=*])
    |:  |-
    ^-  a
    ?~  b
      a
    $(b t.b, a (put p.i.b q.i.b))
  ::::
  ::
      get
    ^<  mine.[!=(->) %get ~]
    =+  b=*
    |:  |-
    ^-  *<~ [~ u=?>(?=(^ a) q.n.a)]>
    ?~  a
      ~
    ?:  .=(b p.n.a)
      [~ u=q.n.a]
    ?:  (gor b p.n.a)
      $(a l.a)
    $(a r.a)
  ::::
  ::
      has
    ^<  mine.[!=(->) %has ~]
    =+  b=*
    |:
    !.=(~ (get b))
  ::::
  ::
      put
    ^<  mine.[!=(->) %put ~]
    =+  [b=* c=*]
    |:
    ^-  a
    ?~  a
      [[b c] ~ ~]
    ?:  .=(b p.n.a)
      ?.  .=(c q.n.a)
        ^>(bean.%put-by !!)
      a
    ?:  (gor b p.n.a)
      =+  d=$(a l.a)
      ?>  ?=(^ d)
      ?:  (vor p.n.a p.n.d)
        [n.a d r.a]
      [n.d l.d [n.a r.d r.a]]
    =+  d=$(a r.a)
    ?>  ?=(^ d)
    ?:  (vor p.n.a p.n.d)
      [n.a l.a d] 
    [n.d [n.a l.a l.d] r.d]
  ::::
  ::
      tap
    =+  b=*(list &[p=* q=*])
    |:  |-
    ^-  b
    ?~  a
      b
    $(a r.a, b [n.a $(a l.a)])
  ::
  ::::
  ==
::
::::
  ::  Tier 5, parsing and printing
::::
::
    byte  &@
    tape  (list byte)
    rock  &@
    term  &@
    wall  (list tape)
::::
::
    ball
  |?
    ^+(@ *rock)
    [%duct p=*tape]
    [%grip p=[p=*rock q=*rock] q=*(list ball)]
    [%meld p=*(list ball)]
  --
::::
::
    to
  ^<    mine.
      :+  !=(->) 
        %to 
      :~
        [%fly !=(fly)]
        [%ram !=(ram)]
        [%wit !=(wit)]
      --
  =+  pup=*ball
  |*
  ::::
  ::        fly: print tall
      fly
    ^<  mine.[!=(->) %fly ~]
    =+  bor=@
    |=
    ^-  *wall
    =+  col=@
    =>  |*
        ::::
        ::        ind: indent tape
            ind
          =+  tep=*tape
          |=
          ^-  *tape
          ?:  .=(0 col)
            tep
          $(col (dec col), tep [' ' tep])
        ==
    ::::
    =+  lux=*wall
    |-  ^-  *wall
    ?.  ?&(?=([%grip *] pup) (gte (add col wit) bor))
      [(ind ram) lux]
    :-  (ind (rip 3 p.p.pup))
    |-  ^-  *wall
    ?~  q.pup
      [(ind (rip 3 q.p.pup)) lux]
    %=  $$
      pup   i.q.pup
      lux   $(q.pup t.q.pup)
      col   ?:((gte col (mul 2 (div bor 3))) 0 (add 2 col))
    --
  ::::
  ::        ram: print wide
      ram
    ^-  *tape
    ?-    pup 
    ::
        @           (rip 3 pup)
        [%meld *]   (reel p.pup =+([*ball *tape] |=((weld ram(pup -<-) -<+))))
        [%duct *]   p.pup 
    ::
        [%grip *]
      %+  weld
        (rip 3 p.p.pup)
      |-  ^-  *tape
      ?-  q.pup
        ~       (rip 3 q.p.pup)
        [* ~]   (weld ram(pup i.q.pup) (rip 3 q.p.pup))
        ^       (weld ram(pup i.q.pup) `*tape`[' ' $(q.pup t.q.pup)])
      --
    --
  ::::
  ::      wit: width rammed
      wit
    ^-  @
    ?-    pup
        @           (met 3 pup)
        [%duct *]   (lent p.pup)
    ::
        [%grip *]
      %+  add
        (add (met 3 p.p.pup) (met 3 q.p.pup))
      %+  roll
        q.pup
      =+  [*ball @]
      |=((add ?:(.=(0 -<+) 0 1) (add -<+ wit(pup -<-))))
    ::
        [%meld *]   (roll p.pup =+([*ball @] |=((add -<+ wit(pup -<-)))))
    -- 
  ::
  ::::
  ==
::::
::
    go
  =+  nut=*
  |%
  ::::
  ::
      dem
    (num 10 =+(@ |=((add '0' -<))))
  ::::
  ::
      gut
    |-  ^-  *ball
    ?-  nut
        @   unt(nut nut)
        ^
      :+  %grip
        ['[' ']']
      |-  ^-  *(list ball)
      ?-  nut
        [* @]   [$$(nut -.nut) $$(nut +.nut) ~]
        [* ^]   [$$(nut -.nut) $(nut +.nut)]
      --
    --
  ::::
  ::
      ham
    (num 16 =+(@ |=(?:((lth 10 -<) (add '0' -<) (add 'a' -<)))))
  ::::
  ::
      loc
    ^-  *ball
    ?>  ?=([p=@ q=[p=[p=@ q=@] q=[p=@ q=@]]] nut)   :: should be static
    :+  %grip
      ['<' '>']
    =+  ^=  rov
        :~  [%grip ['[' ']'] dem(nut p.p.q.nut) dem(nut q.p.q.nut) ~]
            [%grip ['[' ']'] dem(nut p.q.q.nut) dem(nut q.q.q.nut) ~]
        --
    ?:  .=(%% p.nut)
      rov
    [p.nut rov]
  ::::
  ::
      num
    =+  [zel=@ hoz==+(@ |=(@))]
    |:
    ^-  [%duct p=*tape]
    :-  %duct
    ?:  .=(0 nut)
      [(hoz 0) ~]
    =+  biq=`*tape`~
    |-  ^-  *tape
    ?:  .=(0 nut)
      biq
    $(nut (div nut zel), biq [(hoz (mod nut zel)) biq])
  ::::
  ::
      tem
    ^-  [%duct p=*tape]
    :-  %duct
    |-  ^-  *tape
    ?:  .=(0 nut)
      ~
    [(end 3 1 nut) $(nut (rsh 3 1 nut))]
  ::::
  ::
      tis
    [%duct p=nut]
  ::::
  ::
      unt
    ^-  [%duct p=*tape]
    ?:  ?&  (gte (met 3 nut) 2)
            |-
            ?:  .=(0 nut)
              &
            =+  vis=(end 3 1 nut)
            =+  gor=(rsh 3 1 nut)
            ?&(?|(.=('-' vis) ?&((gte vis 'a') (lte vis 'z'))) $(nut gor))
        --
      =<([- ['%' +]] tem)
    ?:  (lte (met 3 nut) 2)
      dem
    ham
  ::
  ::::
  ==
::
::::
  ::  Tier 6, Watt in Watt
::::
::
::
::  molds
::
::::
::
    gene
  |?
    ^+(@ *term)
    [~ p=^+(@ *axis)]
  ::
    [%bndl p=*gene q=*gene]                                     ::  =<  bondul
    [%bndp p=*gene q=*gene]                                     ::  =-  bondap
    [%bnpd p=*gene q=*gene]                                     ::  =+  bonpad
    [%bnld p=*gene q=*gene]                                     ::  =>  bonlud
  ::
    [%brbn p=*gene]                                             ::  |=  barbon
    [%brcs p=*(list gene)]                                      ::  |?  barcas
    [%brdg p=*gene]                                             ::  |:  bardeg
    [%brdp p=*gene]                                             ::  |-  bardap
    [%brdl p=*gene q=*gene]                                     ::  |<  bardul
    [%brld p=*gene q=*gene]                                     ::  |>  barlud
    [%brmt p=*(list &[p=*term q=*gene])]                        ::  |%  barmit
    [%brsg p=*gene q=*(list gene)]                              ::  |~  barsig
    [%brtr p=*(list &[p=*term q=*gene])]                        ::  |*  bartar
  ::
    [%csbn p=*gene q=*gene]                                     ::  ?=  casbon
    [%csbr p=*(list gene)]                                      ::  ?|  casbar
    [%csdg p=*gene q=*gene r=*gene]                             ::  ?:  casdeg
    [%csdl p=*gene q=*gene]                                     ::  ?<  casdul
    [%csdt p=*gene q=*gene r=*gene]                             ::  ?.  casdot
    [%csld p=*gene q=*gene]                                     ::  ?>  caslud
    [%csdp p=*gene q=*(list &[p=*gene q=*gene])]                ::  ?-  casdap
    [%cspm p=*(list gene)]                                      ::  ?&  caspam
    [%cstr p=*gene q=*(list gene)]                              ::  ?*  castar
    [%cssg p=*gene q=*gene r=*gene]                             ::  ?~  cassig
    [%cszp p=*gene]                                             ::  ?!  caszap
  ::
    [%dgkt p=*gene q=*gene r=*gene s=*gene]                     ::  :^  degket
    [%dgdp p=*gene q=*gene]                                     ::  :-  degdap
    [%dgpd p=*gene q=*gene r=*gene]                             ::  :+  degpad
    [%dgtr p=*(list gene)]                                      ::  :*  degtar
    [%dgsg p=*(list gene)]                                      ::  :~  degsig
  :: 
    [%dtbn p=*gene q=*gene]                                     ::  .=  dotbon
    [%dtkt p=*gene]                                             ::  .^  dotket
    [%dtcs p=*gene]                                             ::  .?  dotcas
    [%dttr p=*gene q=*gene]                                     ::  .*  dottar
    [%dtsg p=*]                                                 ::  .~  dotsig
  ::
    [%ktbn p=*term q=*gene]                                     ::  ^=  ketbon
    [%ktdl p=*<@ [p=@ q=*gene]> q=*gene]                        ::  ^<  ketdul
    [%ktdp p=*gene q=*gene]                                     ::  ^-  ketdap
    [%ktpd p=*gene q=*gene]                                     ::  ^+  ketpad
    [%ktld p=*<@ [p=@ q=*gene]> q=*gene]                        ::  ^>  ketlud
    [%ktsg p=*gene]                                             ::  ^~  ketsig
  ::
    [%mtbn p=*rope q=*(list &[p=*gene q=*gene])]                ::  %=  mitbon
    [%mtbr p=*rope q=*gene r=*(list &[p=*gene q=*gene])]        ::  %|  mitbar
    [%mtdg p=*gene q=*gene]                                     ::  %:  mitdeg
    [%mtdt p=*gene q=*gene]                                     ::  %.  mitdot
    [%mtkt p=*gene q=*gene r=*gene s=*gene]                     ::  %^  mitket
    [%mtdp p=*gene q=*(list gene)]                              ::  %-  mitdap  
    [%mtpd p=*gene q=*gene r=*gene]                             ::  %+  mitpad
    [%mttr p=*gene]                                             ::  %*  mittar
    [%mtsg p=*rope q=*gene r=*gene]                             ::  %~  mitsig
  ::
    [%tmbn p=*<%atom %blur %cell %flag %null>]                  ::  ;=  tambon
    [%tmdp p=*gene]                                             ::  ;-  tamdap
    [%tmpd p=*gene q=*gene]                                     ::  ;+  tampad
  ::
    [%zpbn p=*gene]                                             ::  !=  zapbon
    [%zpdx p=*gene]                                             ::  !#  zapdax
    [%zpcb p=*spot q=*gene]                                     ::  !_  zapcab
    [%zpzp ~]                                                   ::  !!  zapzap
  --
  ::
::::
::
    plan  &[p=*axis q=*(unit &[p=*axis q=*(list gene)]) r=*type]
::::
::
    rope  (list <^+(@ *term) [~ p=*axis]>)
::::
::
    spot  &[p=[p=@ q=@] q=[p=@ q=@]]
::::
::
    tool
  |?
    [0 p=@]
    [1 p=*]
    [2 p=*tool q=*tool]
    [3 p=*tool]
    [4 p=*tool]
    [5 p=*tool q=*tool]
    [6 p=*tool q=*tool r=*tool]
    [7 p=*tool q=*tool]
    [8 p=*tool q=*tool]
    [9 p=@ q=*tool]
    [10 p=*<@ [p=@ q=*tool]> q=*tool]
    [p=^+(^ *tool) q=*tool]
  --
::::
::
    cone
  |?
    :-  q=*<%blue [%gray p=*type] %pink [%teal p=*type]> 
      r=[p=* q=*(book term gene)]
  --
::::
::
    type
  |?
    %atom
    %blur
    %blot
    [%cell p=*type q=*type]
    [%core p=*type *cone]
  ::
    [%cube p=*]
    [%face p=*term q=*type]
    [%fork p=*type q=*type]
    [%hold p=*type q=*(list gene)]
  --
::
::  gears
::
  ::
::::
::
    cell
  ^<  mine.[!=(->) %cell ~]
  =+  [hed=*type tal=*type]
  |=
  ^-  *type
  ?:  .=(%blot hed)
    %blot
  ?:  .=(%blot tal)
    %blot
  [%cell hed tal]
::::
::
    comb
  ^<  mine.[!=(->) %comb ~]
  =+  [mal=*tool buz=*tool]
  |=
  ^>  bean.%comb
  ^-  *tool
  ?:  ?&(?=([0 *] mal) !.=(0 p.mal))
    ?:  ?&(?=([0 *] buz) !.=(0 p.buz)) 
      [0 (peg p.mal p.buz)]
    ?:  ?=([2 [0 *] [0 *]] buz)
      [2 [0 (peg p.mal p.p.buz)] [0 (peg p.mal p.q.buz)]]
    [7 mal buz]
  ?:  ?=([^ [0 1]] mal)
    [8 p.mal buz]
  ?:  .=([0 1] buz)
    mal
  [7 mal buz]
::::
::
    core
  ^<  mine.[!=(->) %core ~]
  =+  :*  vod=*type 
          suz=*type
          hel=*<%blue [%gray p=*type] %pink [%teal p=*type]> 
          don=[p=* q=*(book term gene)]
      --
  |=
  ^-  *type
  ?:  .=(%blot vod)
    %blot
  :^    %core
      vod
    ?-    hel
        %blue       ?:(.=(vod suz) %blue [%gray suz])
        [%gray *]   ?:(.=(vod p.hel) %blue hel)
        %pink       ?:(.=(vod suz) %pink [%teal suz])
        [%teal *]   ?:(.=(vod p.hel) %pink hel)
    --
  don
::::
::
    cons
  ^<  mine.[!=(->) %cons ~]
  =+  [vur=*tool sed=*tool]
  |=
  ^>  bean.%cons
  ^-  *tool
  ?:  ?=([[0 *] [0 *]] -<)
    ?:  ?&(.=(.^(p.vur) p.sed) .=((div p.vur 2) (div p.sed 2)))
      [0 (div p.vur 2)]
    [vur sed]
  ?:  ?=([[1 *] [1 *]] -<)
    [1 p.vur p.sed]
  [vur sed] 
::::
::
    fork
  ^<  mine.[!=(->) %fork ~]
  =+  [hoz=*type bur=*type]
  |=
  ^-  *type
  ?:  .=(hoz bur)
    hoz
  ?:  .=(%blot hoz)
    bur
  ?:  .=(%blot bur)
    hoz
  [%fork hoz bur]
::::
::
    flag
  ^-(*type [%fork [%cube 0] [%cube 1]])
::::
::
    flan
  ^<  mine.[!=(->) %flan ~]
  =+  [bos=*tool nif=*tool]
  |=
  ^-  *tool
  ?-    bos
      [1 1]   bos
      [1 0]   nif
      *
    ?-    nif
        [1 1]   nif
        [1 0]   bos
        *       [6 bos nif [1 1]]
    --
  --
::::
::
    flip
  ^<  mine.[!=(->) %flip ~]
  =+  [hel=*tool]
  |=
  [6 hel [1 1] [1 0]]
::::
::
    flor
  ^<  mine.[!=(->) %flor ~]
  =+  [bos=*tool nif=*tool]
  |=
  ^-  *tool
  ?-  bos
      [1 1]   nif
      [1 0]   bos
      *
    ?-  nif
        [1 1]   bos
        [1 0]   nif
        *       [6 bos [1 0] nif]
    --
  --
::::
::
    hack
  ^<  mine.[!=(->) %hack ~]
  =+  gen=*gene
  |=
  ^-  *<[& p=*gene q=*gene] [| p=*gene]>
  ?-    gen
      [%bnld *]
    ?.  ?=([~ *] p.gen)
      [| gen] 
    =+  pyr=$(gen q.gen)
    ?-    -.pyr
        |   [| [%bnld p.gen p.pyr]]
        &   [& [%bnld p.gen p.pyr] [%bnld p.gen q.pyr]]
    --
  ::
      [%dgdp *]   [& p.gen q.gen]
      *
    =+  voq=(open gen)
    ?:  .=(gen voq)
      [| gen]
    $(gen voq)
  ::
      [%zpcb *] 
    =+  pyr=$(gen q.gen)
    ?-    -.pyr
        |   [| [%zpcb p.gen p.pyr]]
        &   [& [%zpcb p.gen p.pyr] [%zpcb p.gen q.pyr]]
    --
  --
::::
::
    hike 
  ^<  mine.[!=(->) %hike ~]
  =+  [axe=*axis pac=*(list &[p=*axis q=*tool])]
  |=
  ^>  bean.%hike
  ^-  *tool
  ?~  pac
    [0 axe]
  =+  zet=(skim ^@(pac) =+([p=*axis q=*tool] |=([.=(1 p)])))
  ?~  zet
    =+  tum=(skim ^@(pac) =+([p=*axis q=*tool] |=(?&(!.=(1 p) .=(2 (cap p))))))
    =+  gam=(skim ^@(pac) =+([p=*axis q=*tool] |=(?&(!.=(1 p) .=(3 (cap p))))))
    %+  cons
      %=  $
        axe (peg axe 2)
        pac (turn tum =+([p=*axis q=*tool] |=([(mas p) q])))
      --
    %=  $
      axe (peg axe 3)
      pac (turn gam =+([p=*axis q=*tool] |=([(mas p) q])))
    --
  ?>(?=([* ~] zet) q.i.zet)
::::
::
    look
  ^<  mine.[!=(->) %look ~]
  =+  [cog=*term dab=*(book term gene)]
  |= 
  =+  axe=`@`1
  |-
  ^-  *(unit &[p=*axis q=*gene])
  ?-  dab
      ~   ~
  ::
      [* ~ ~]   
    ?:(.=(cog p.n.dab) [~ axe q.n.dab] ~)
  ::
      [* ~ *]
    ?:  .=(cog p.n.dab)
      [~ (peg axe 2) q.n.dab]
    ?:  (gor cog p.n.dab)
      ~
    $(axe (peg axe 3), dab r.dab)
  ::
      [* * ~]
    ?:  .=(cog p.n.dab)
      [~ (peg axe 2) q.n.dab]
    ?:  (gor cog p.n.dab)
      $(axe (peg axe 3), dab l.dab)
    ~
  ::
      [* * *]
    ?:  .=(cog p.n.dab)
      [~ (peg axe 2) q.n.dab]
    ?:  (gor cog p.n.dab)
      $(axe (peg axe 6), dab l.dab)
    $(axe (peg axe 7), dab r.dab)
  --
::::
::
    face
  ^<  mine.[!=(->) %face ~]
  =+  [cog=*term der=*type] 
  |=
  ^-  *type
  ?:  .=(%blot der)
    %blot
  [%face cog der]
::::
::
    open
  ^<  mine.[!=(->) %open ~]
  =+  gen=*gene
  |=
  ^>  bean.%open
  ^-  *gene
  =<  base
  |*
  ::::
  ::
      base
    ?-    gen
        @           [%mtbn [gen ~] ~]
        [~ *]       [%mtbn [gen ~] ~]
    ::::
    ::::
        [%bndl *]   [%bnld q.gen p.gen]                                 ::  =<
        [%bndp *]   [%bnpd q.gen p.gen]                                 ::  =-
        [%bnpd *]   [%bnld [%dgdp p.gen [~ 1]] q.gen]                   ::  =+
    ::::
    ::::
        [%brbn *]   [%brtr [[%% p.gen] ~]]                              ::  |=
        [%brcs *]                                                       ::  |?
      ?~  p.gen
        [%zpzp ~]
      [%brld [%ktdp [%tmbn %blur] i.p.gen] [%cstr [~ 4] p.gen]]
    ::
        [%brdg *]   [%brmt [[%% p.gen] ~]]                              ::  |:
        [%brdl *]   [%bnpd p.gen [%brdg q.gen]]
        [%brld *]   [%bnpd p.gen [%brbn q.gen]]
        [%brdp *]   [%bnld [%brbn p.gen] %%]                            ::  |-
        [%brsg *]                                                       ::  |~
      |-
      ?-  q.gen
          ~       !!
          [* ~]   i.q.gen
      ::
          ^
        :+  %bnpd
          $(q.gen t.q.gen)
        :+  %bnpd
          [%bnld [~ 3] i.q.gen]
        :+  %bnpd
          [~ 8]
        :-  %brdg
        :^    %mtpd
            [%bnld [~ 23] p.gen]
          [%mtdp [~ 10] [~ 4] ~]
        [%mtbn [[~ 22] ~] [[[~ 4] [~ 4]] ~]]
      --
    ::::
    ::::
        [%csbr *]                                                       ::  ?|
      |- 
      ?~(p.gen [%dtsg 1] [%csdg i.p.gen [%dtsg 0] $(p.gen t.p.gen)])
    ::
        [%csdl *]   [%csdg p.gen [%zpzp ~] q.gen]                       ::  ?<
        [%csdt *]   [%csdg p.gen r.gen q.gen]                           ::  ?.
        [%csld *]   [%csdg p.gen q.gen [%zpzp ~]]                       ::  ?>
        [%csdp *]                                                       ::  ?-
      |-
      ?~  q.gen
        [%zpzp ~]
      :^    %csdg
          [%csbn (sift p.i.q.gen) p.gen]
        [%bnld [%mtbn ~ [[p.gen [%ktpd p.i.q.gen p.gen]] ~]] q.i.q.gen]
      $(q.gen t.q.gen)
    ::
        [%cspm *]                                                       ::  ?&
      |-
      ?~(p.gen [%dtsg 0] [%csdg i.p.gen $(p.gen t.p.gen) [%dtsg 1]])
    ::
        [%cstr *]                                                       ::  ?*
      |-
      ?~  q.gen
        [%zpzp ~]
      :^    %csdg
          [%csbn (sift i.q.gen) [%mtbn (rake p.gen) ~]]
        (mold (rake p.gen) i.q.gen)
      $(q.gen t.q.gen)
    ::
        [%cssg *]                                                       ::  ?~
      [%csdp p.gen [[[%dtsg 0] q.gen] [[%tmbn %cell] r.gen] ~]]
    ::
        [%cszp *]   [%csdg p.gen [%dtsg 1] [%dtsg 0]]                   ::  ?!
    ::::
    ::::
        [%dgkt *]   [%dgdp p.gen [%dgdp q.gen [%dgdp r.gen s.gen]]]     ::  :^
        [%dgpd *]   [%dgdp p.gen [%dgdp q.gen r.gen]]                   ::  :+
        [%dgsg *]                                                       ::  :~
      |-
      ?~(p.gen [%dtsg ~] [%dgdp i.p.gen $(p.gen t.p.gen)])
    ::
        [%dgtr *]                                                       ::  :*
      |-
      ?-    p.gen 
          ~       [%zpzp ~]
          [* ~]   i.p.gen
          ^       [%dgdp i.p.gen $(p.gen t.p.gen)]
      --
    ::::
    ::::
        [%ktdl *]   [%bndl [%ktld p.gen [~ 1]] q.gen]                   ::  ^<
    ::::
    ::::
        [%mtbr *]                                                       ::  %|
      :+  %bnpd
        q.gen
      :+  %mtbn
        (weld p.gen `*rope`[[~ 2] ~])
      (turn r.gen =+([*gene *gene] |=([-<- [%bnld [~ 3] -<+]])))
    ::
        [%mtdg *]   [%mtsg [%% ~] p.gen q.gen]                          ::  %:
        [%mtdt *]   [%mtdp q.gen [p.gen ~]]                             ::  %.
        [%mtkt *]   [%mtdp p.gen q.gen r.gen s.gen ~]                   ::  %^
        [%mtdp *]                                                       ::  %-  
      ?~(q.gen [%bnld p.gen %%] [%mtdg p.gen [%dgtr q.gen]])
    ::                                                                  ::  %+
        [%mtpd *]   [%mtdp p.gen q.gen r.gen ~]
        [%mttr *]   [%bnld p.gen [%mtbn [%% ~] ~]]                      ::  %*
        [%mtsg *]   [%mtbr p.gen q.gen [[[~ 4] r.gen] ~]]               ::  %~
    ::::
    ::::
        [%tmbn *]                                                       ::  ;=
      ?-  p.gen
        %atom   [%ktdp [%dtkt %dtsg 0] [%dtsg 0]]
        %blur   [%ktdp [%dttr [%dtsg 0] [%dtsg 0]] [%dtsg 0]]
        %cell   [%dgdp [%tmbn %blur] [%tmbn %blur]]
        %flag   [%ktdp [%dtcs %dtsg 0] [%dtsg 0]]
        %null   [%dtsg 0]
      --
    ::
        [%tmdp *]   (sift p.gen)                                        ::  ;-
        [%tmpd *]   (mold (rake q.gen) p.gen)                           ::  ;+
    ::::
    ::::
        *           gen
    --
  ::::
  ::
      mold
    =+  [hep=*rope gen=*gene]
    |=
    ^-  *gene
    ?-    gen
        [%dgdp *]
      :+  %dgdp
        $(gen p.gen, hep [[~ 2] hep])
      $(gen q.gen, hep [[~ 3] hep])
    :: 
        [%ktbn *]   [%ktbn p.gen $(gen q.gen)]
        [%ktdp *]   $(gen p.gen)
        [%ktpd *]   [%ktpd p.gen $(gen q.gen)]
        [%mttr *]   [%mtdp p.gen [%mtbn hep ~] ~]
        [%zpcb *]   [%zpcb p.gen $(gen q.gen)]
        [%zpdx *]   [%zpdx $(gen p.gen)]
        *
      =+  bog=(open gen)
      ?.  .=(bog gen)
        $(gen bog)
      [%csld [%csbn gen [%mtbn hep ~]] [%mtbn hep ~]]
    --
  ::::
  ::
      sift
    =+  [gen=*gene]
    |=
    ^-  *gene
    ?-  gen
        [%dgdp *]   [%dgdp $(gen p.gen) $(gen q.gen)]
        [%dtsg *]   gen
        [%ktbn *]   [%ktbn p.gen $(gen q.gen)]
        [%ktpd *]   $(gen p.gen)
        [%mttr *]   
      ?.  ?=([%brcs *] p.gen)
        [%tmbn %blur]
      gen(p.p (turn p.p.gen =+(*gene |=($$(gen -<)))))
    ::
        [%tmbn *]   gen
        [%tmdp *]   $(gen p.gen)                            ::  XX delete
        [%zpcb *]   [%zpcb p.gen $(gen q.gen)]
        [%zpdx *]   [%zpdx $(gen p.gen)]
        *
      =+  bog=(open gen)
      ?:  .=(bog gen)
        [%tmbn %blur]
      $(gen bog)
    --
  ::
  ::::
  ==
::::
::
    rake
  ^<  mine.[!=(->) %rake ~]
  =+  gen=*gene
  |=
  ^>  bean.%rake
  ^-  *rope
  ?-  gen
    @             [gen ~]
    [~ *]         [gen ~]
    [%mtbn * ~]   p.gen
    [%zpcb *]     $(gen q.gen)
    [%zpdx *]     $(gen p.gen)
  --
::
    ream  ^<(mine.[!=(->) %ream ~] =+(txt=@ |=(^-(*gene [%ream !!]))))
::::
::
    ut
  ^<    mine.
      :+  !=(->+) 
        %ut 
      :~
        [%fan !=(fan)]
        [%rib !=(rib)]
        [%vet !=(vet)]
        [%dump !=(dump)]
        [%find !=(find)]
        [%fish !=(fish)]
        [%gain !=(gain)]
        [%nest !=(nest)]
        [%play !=(play)]
        [%mill !=(mill)]
        [%orth !=(orth)]
        [%peek !=(peek)]
        [%repo !=(repo)]
        [%ripe !=(ripe)]
        [%seek !=(seek)]
        [%snap !=(snap)]
        [%tuck !=(tuck)]
      --
  =+  :*  fan=*(pool type)
          rib=*(pool &[*type *type *gene])
          vet=`?`&
      --
  =+  sut=`*type`%blur
  |*
  ::::
  ::
      burn
    |-
    ^-  [p=*type q=*]
    ?-    sut
        %atom       [%atom 0]
        %blot       ^>(bean.%blot-burn !!)
        %blur       [%blur 0]
        [%cell *]   
      =+  [hed=$(sut p.sut) tal=$(sut q.sut)]
      [(cell p.hed p.tal) [q.hed q.tal]]
    ::
        [%core *]
      =+  dan=$(sut p.sut)
      ?-    p.r.sut
          &   [(cell p.dan %blur) [q.dan 0]]
          *   [(core p.dan p.sut q.sut r.sut) [q.dan p.r.sut]]
      --
    ::
        [%cube *]   [sut p.sut]
        [%face *]   =+(fos=$(sut q.sut) [(face p.sut p.fos) q.fos])
        [%fork *]   =+(fos=$(sut p.sut) [(fork p.fos q.sut) q.fos])
        [%hold *]   $(sut repo)
    --
  ::::
  ::
      cull
    ^<  mine.[!=(->) %cull ~]
    =+  [axe=*axis ref=*type]
    |=
    ^>  bean.%cull
    ^-  *type
    ?:  .=(1 axe)
      (fuse ref)
    =+  [now=(cap axe) lat=(mas axe)]
    |-  ^-  *type
    ?-    sut
        %atom       %blot
        %blot       %blot
        %blur       (reco =+(p=*type |=($$(sut p))))
        [%cell *]   
      ?:  .=(2 now)
        (cell $$(axe lat, sut p.sut) q.sut)
      (cell p.sut $$(axe lat, sut q.sut))
    ::
        [%core *]
      ?.(.=(2 now) sut (core $$(axe lat, sut p.sut) p.sut q.sut r.sut))
    ::
        [%cube *]   ?.(.?(p.sut) %blot (reco =+(p=*type |=($$(sut p)))))
        [%face *]   (reco =+(p=*type |=((face p.sut $$(sut p)))))
        [%fork *]   (fork $(sut p.sut) $(sut q.sut))
        [%hold *]   (reco =+(p=*type |=($$(sut p))))
    --
  ::::
  ::
      dirt
    ?>  ?=([%core *] sut)
    ?-    q.sut
        %blue       sut
        [%gray *]   [%core p=p.q.sut q=%blue r=r.sut]
        %pink       sut
        [%teal *]   [%core p=p.q.sut q=%pink r=r.sut]
    --
  ::::
  ::
      dump
    ^-  *ball
    ^>  bean.%dump
    =+  ^=  gil
        =+  den=[p=`@`1 q=*(book type &@)]
        =+  vax=*(pool type)
        =<  q
        |-
        ^-  den
        ?-    sut
            [%cell *]   $(sut q.sut, den $(sut p.sut))
        ::
            [%core *]   
          ?:  ?=(^ q.sut) 
            $(sut p.sut, den $(sut p.q.sut))
          $(sut p.sut)
        ::
            [%face *]   $(sut q.sut)
            [%fork *]   $(sut q.sut, den $(sut p.sut))
        ::
            [%hold *]
          ?:  (~(has in vax) sut)
            ?:  (~(has by q.den) sut)
              den
            [.^(p.den) (~(put by q.den) [sut p.den])]
          $(vax (~(put in vax) sut), sut repo)
        ::
            *           den
        --
    =+  gax=*(pool type)
    ^>  bean.%dump-main
    |-
    ?-    sut
        %atom       '@'
        %blot       '#'
        %blur       '*'
    ::
        [%cell *]
      :+  %grip
        ['[' ']']
      :-  $(sut p.sut)
      |-
      ?.  ?=([%cell *] q.sut)
        [$$(sut q.sut) ~]
      [$$(sut p.q.sut) $(q.sut q.q.sut)]
    ::
        [%core *]
      =+  gep=?:(?=([[%% *] ~ ~] q.r.sut) '~' '=')
      :-  %grip
      ?-    q.sut
          %blue       [[(cat 3 gep '-{') '}'] $(sut p.sut) ~]
          [%gray *]   [[(cat 3 gep '+{') '}'] $(sut p.sut) $(sut p.q.sut) ~]
          %pink       [[(cat 3 gep '|{') '}'] $(sut p.sut) ~]
          [%teal *]   [[(cat 3 gep '&{') '}'] $(sut p.sut) $(sut p.q.sut) ~]
      --
    ::
        [%cube *]   ?:(.=(0 p.sut) '~' ~(gut go p.sut))
        [%face *]   [%meld ~(tem go p.sut) '=' $(sut q.sut) ~]
    ::
        [%fork *]
      ?:  .=([%fork [%cube 0] [%cube 1]] sut)
        '?'
      :+  %grip
        ['<' '>']
      :-  $(sut p.sut)
      |-
      ?.  ?=([%fork *] q.sut)
        [$$(sut q.sut) ~]
      [$$(sut p.q.sut) $(q.sut q.q.sut)]
    ::
        [%hold *]
      =+  piv=(~(get by gil) sut)
      ?~  piv
        $(sut repo)
      :*  %meld
          '%'
          ~(dem go u.piv)
          ~
          ?:  (~(has in gax) sut)
            ~
          [':' $(gax (~(put in gax) sut), sut repo) ~]
      --
    --
  ::::
  ::
      edit
    ^<  mine.[!=(->) %edit ~]
    =+  [mun=*type har=*(list &[p=*gene q=*gene])]
    |=
    ^>  bean.%edit
    |-  ^-  *type
    ?~  har
      mun
    =+  hep=(rake p.i.har)
    $(har t.har, mun (tusk(sut mun) hep sut q.i.har))
  ::::
  ::
      emit
    ^<  mine.[!=(->) %emit ~]
    =+  [axe=*axis mun=*type har=*(list &[p=*gene q=*gene])]
    |=
    ^>  bean.%emit
    =+  luf=*(list &[p=*axis q=*tool])
    |-
    ^-  [p=*type q=*tool]
    ?~  har
      [mun (hike axe luf)]
    =+  hep=(rake p.i.har)
    =+  taq=(tuck(sut mun) hep sut q.i.har)
    $(har t.har, mun p.taq, luf [q.taq luf])
  ::::
  ::
      find
    ^<  mine.[!=(->) %find ~]
    =+  cog=*term
    |=
    =-  ?~  hoq
          ^>  bean.[%port |=((~(fly to ~(tem go `@`cog)) 75))]
          ^>  bean.[%type |=((~(fly to dump) 75))]
          ^>(bean.%find-fail !!)
        u.hoq
    ^=  hoq
    =+  gil=*(pool type)
    |-
    ^-  *(unit plan)
    ?-    sut
    ::
        [%cell *]
      =+  taf=$(sut p.sut)
      ?~  taf
        =+  bov=$(sut q.sut)
        ?~  bov
          ~
        [~ (peg 3 p.u.bov) q.u.bov r.u.bov]
      [~ (peg 2 p.u.taf) q.u.taf r.u.taf]
    ::
        [%core *]
      =+  zem=(look cog q.r.sut)
      ?~  zem
        =+  taf=$(sut p.sut)
        ?~  taf
          ~
        [~ (peg 2 p.u.taf) q.u.taf r.u.taf]
      [~ 1 [~ (peg 3 p.u.zem) [q.u.zem ~]] sut]
    ::
        [%face *]
      ?: .=(cog p.sut)
        [~ 1 ~ q.sut]
      ~
    ::
        [%fork *]
      ^>  bean.%find-fork
      =+  hax=$(sut p.sut)
      =+  yor=$(sut q.sut)
      ?~  hax                     ::  XX - this should be a lot prettier.
        ?~  yor
          ~
        ^>  bean.[%port |=((~(fly to ~(tem go `@`cog)) 75))]
        ^>  bean.[%type |=((~(fly to dump) 75))]
        ^>(bean.%find-fork !!)
      ?~  yor
        ^>  bean.[%port |=((~(fly to ~(tem go `@`cog)) 75))]
        ^>  bean.[%type |=((~(fly to dump) 75))]
        ^>(bean.%find-fork !!)
      ?:  .=(hax yor)
        hax
      ?.  ?&(.=(p.u.hax p.u.yor) .=(.=(~ q.u.hax) .=(~ q.u.yor)))
        ^>  bean.[%port |=((~(fly to ~(tem go `@`cog)) 75))]
        ^>  bean.[%type |=((~(fly to dump) 75))]
        ^>(bean.%find-mate !!)
      ?~  q.u.hax
        [~ p.u.hax ~ (fork r.u.hax r.u.yor)]
      ?>  ?=(^ q.u.yor)
      ?.  ?&(.=(p.u.q.u.yor p.u.q.u.hax)) 
        ^>  bean.[%port |=((~(fly to ~(tem go `@`cog)) 75))]
        ^>  bean.[%type |=((~(fly to dump) 75))]
        ^>(bean.%find-call !!)
      :^    ~
          p.u.hax
        [~ p.u.q.u.hax (weld q.u.q.u.hax q.u.q.u.yor)] 
      (fork r.u.hax r.u.yor)
    ::
        [%hold *]
      ?:  (~(has in gil) sut)
        ~
      $(gil (~(put in gil) sut), sut repo)
    ::
        *
      ~
    --
  ::::
  ::
      fish
    ^<  mine.[!=(->) %fish ~]
    =+  [axe=*axis]
    |=
    =+  vit=*(pool type)
    |-
    ^-  *tool
    ?-  sut
        %atom       (flip [3 0 axe])
        %blot       [1 1]
        %blur       [1 0]
    ::
        [%cell *]
      %+  flan
        [3 0 axe]
      (flan $(sut p.sut, axe (peg axe 2)) $(sut q.sut, axe (peg axe 3)))
    ::
        [%core *]   [0 0]
        [%cube *]   [5 [1 p.sut] [0 axe]]
        [%face *]   $(sut q.sut)
    ::
        [%fork *]
      (flor $(sut p.sut) $(sut q.sut))
    ::
        [%hold *]
      ?:  (~(has in vit) sut)
        ^>  bean.[%type |=((~(fly to dump) 75))]
        ^>  bean.[%axis |=((~(fly to ~(gut go `*`axe)) 75))]
        ^>(bean.%fish-hold !!)
      =>  %=(. vit (~(put in vit) sut))
      $(sut repo)
    --
  ::::
  ::
      fuse
    ^<  mine.[!=(->) %tine ~]
    =+  ref=*type
    |=
    ^>  bean.%fuse
    =+  bix=*(pool &[*type *type])
    |-  ^-  *type
    ?:  .=(sut ref)
      sut
    ?-    sut
        %atom 
      ?-    ref
          [%cell *]   %blot
          *           $(sut ref, ref sut)
      --
    ::
        %blur       ref
        %blot       %blot
        [%cell *]   
      ?-    ref
          [%cell *]   (cell $(sut p.sut, ref p.ref) $(sut q.sut, ref q.ref))
          *           $(sut ref, ref sut)
      --
    ::
        [%core *]   $(sut repo)
        [%cube *]
      ?-    ref
          %atom       ?:(.?(p.sut) %blot sut)
          [%cell *]   ?:(.?(p.sut) $(sut repo) %blot)
          [%cube *]   %blot
          *           $(sut ref, ref sut)
      --
    ::
        [%face *]     (face p.sut $(sut q.sut))
        [%fork *]     (fork $(sut p.sut) $(sut q.sut))
        [%hold *]
      ?:  (~(has in bix) [sut ref])
        ^>  bean.[%fuse-sut |=((~(fly to dump) 75))]
        ^>  bean.[%fuse-ref |=((~(fly to dump(sut ref)) 75))]
        ^>  bean.%fuse-loop 
        !!
      $(bix (~(put in bix) [sut ref]), sut repo)
    --
  ::::
  ::
      gain
    ^<  mine.[!=(->) %gain ~]
    =+  [gen=*gene]
    |=
    ^-  *type
    =-  ?:((orth(sut %blur) fiz) %blot fiz)
    ^=  fiz
    ?-    gen
        [%csbn *]   (cull p:(seek (rake q.gen)) (play p.gen))
        [%cspm *]   |-(?~(p.gen sut $(p.gen t.p.gen, sut $$(gen i.p.gen))))
        [%ktdl *]   $(gen q.gen)
        [%ktld *]   $(gen q.gen)
        [%zpcb *]   $(gen q.gen)
        [%zpdx *]   $(gen p.gen)
        *           sut
    --
  ::::
  ::
      heal
    ^<  mine.[!=(->) %heal ~]
    =+  [cog=*term axe=*axis ref=*type]
    |=
    ^-  *type
    ?-    sut
        [%cell *]
      ?<  .=(1 axe)
      =+  [tip=(cap axe) tap=(mas axe)]
      ?:  .=(2 tip)
        (cell $(sut p.sut, axe tap) q.sut)
      ?>  .=(3 tip)
      (cell p.sut $(sut q.sut, axe tap))
    ::
        [%core *]
      ?:  .=(1 axe)
        ref
      =+  [tip=(cap axe) tap=(mas axe)]
      ?>  .=(2 tip)
      :^    %core
          $(sut p.sut, axe tap)
        ?-    q.sut
            %blue       [%gray p.sut]
            [%gray *]   q.sut
            %pink       [%teal p.sut]
            [%teal *]   q.sut
        --
      r.sut
    ::
        [%face *]   (face p.sut ref)
        [%fork *]   (fork $(sut p.sut) $(sut q.sut))
        [%hold *]   $(sut repo)
    --
  ::::
  ::
      haul
    ^<  mine.[!=(->) %heal ~]
    =+  [axe=*axis ref=*type]
    |=
    ^-  *type
    ?:  .=(1 axe)
      ref
    =+  [tip=(cap axe) tap=(mas axe)]
    ?-    sut
        %blur       $(sut [%cell %blur %blur])
        [%cell *]   
      ?:  .=(2 tip) 
        (cell $(sut p.sut, axe tap) q.sut)
      (cell p.sut $(sut q.sut, axe tap))
    ::
        [%core *]
      ?>  .=(2 tip) 
      :^    %core
          $(sut p.sut, axe tap)
        ?-    q.sut
            %blue       [%gray p.sut]
            [%gray *]   q.sut
            %pink       [%teal p.sut]
            [%teal *]   q.sut
        --
      r.sut
    ::
        [%cube *]   
      ?>  .?(p.sut) 
      $(sut [%cell [%cube -.p.sut] [%cube +.p.sut]])
    ::
        [%face *]   (face p.sut $(sut q.sut))
        [%fork *]   (fork $(sut p.sut) $(sut q.sut))
        [%hold *]   $(sut repo)
        *           %blot
    --
  ::::
  ::
      mill
::    ^>  bean.[%mo-mill |=((~(fly to ~(tem go ?-(gen @ gen, ^ -.gen))) 75))]
    =+  [gol=*type gen=*gene]
    |=
    ^-  [p=*type q=*tool]
    =<  $
    |*
    ::::
    ::
        nice
      =+  typ=*type
      |=
      ?.  ?|(!vet (nest(sut gol) typ))
        ^>  bean.[%need |=((~(fly to dump(sut gol)) 75))]
        ^>  bean.[%have |=((~(fly to dump(sut typ)) 75))]
        ^>(bean.'type-fail' !!)
      typ
    ::::
    ::
        bake
      =+  [dab=*(book term gene)]
      |=
      =>  .(gol %blur)
      ^-  *
      ?-    dab
          ~         0
          [* ~ ~]   q:$$(gen q.n.dab)
          [* ~ *]   [q:$$(gen q.n.dab) $(dab r.dab)]
          [* * ~]   [q:$$(gen q.n.dab) $(dab l.dab)]
          [* * *]   [q:$$(gen q.n.dab) $(dab l.dab) $(dab r.dab)] 
      --
    ::::
    ::
        $
      ^-  [p=*type q=*tool]
      ?-    gen
          [%bnld *]                                                   ::  =>
        =+  fid=$(gen p.gen, gol %blur)
        =+  soq=$(sut p.fid, gen q.gen)
        [p.soq (comb q.fid q.soq)]
      ::
          [%brmt *]                                                   ::  |%
        =+  sac=(~(gas by *(book term gene)) p.gen)
        =+  toc=[%core p=sut q=%pink r=[p=& q=sac]]
        =+  dez=(bake(sut toc, vet |) sac)
        [(nice toc(p.r dez)) [[0 1] [1 dez]]]
      ::
          [%brtr *]                                                   ::  |*
        =+  sac=(~(gas by *(book term gene)) p.gen)
        =+  toc=[%core p=sut q=%blue r=[p=& q=sac]]
        =+  dez=(bake(sut toc) sac)
        [(nice toc(p.r dez)) [[0 1] [1 dez]]]
      ::
          [%csbn *]                                                   ::  ?=
        :-  (nice flag)
        =+  vol=q:$(gen q.gen, gol %blur)
        ?.  ?=([0 *] vol)
          ^>(bean.'casbon-form' !!)
        (fish(sut (play p.gen)) p.vol)
      ::
          [%csdg *]                                                   ::  ?:
        =+  nor=$(gen p.gen, gol flag)
        =+  fex=(gain p.gen)
        ?:  .=(%blot fex)
          $(gen r.gen)
        =+  hiq=$(sut fex, gen q.gen)
        =+  ran=$(gen r.gen)
        [(fork p.hiq p.ran) [6 q.nor q.hiq q.ran]]
      ::
          [%dgdp *]                                                   ::  :-
        =+  hed=$(gen p.gen, gol %blur)
        =+  tal=$(gen q.gen, gol %blur)
        [(nice (cell p.hed p.tal)) (cons q.hed q.tal)]
      ::
          [%dtbn *]                                                   ::  .=
        :-  (nice flag)
        [5 q:$(gen p.gen, gol %blur) q:$(gen q.gen, gol %blur)]
      ::
          [%dtcs *]                                                   ::  .?
        :-  (nice flag)
        [3 q:$(gen p.gen, gol %blur)]
      ::
          [%dtkt *]                                                   ::  .^
        :-  (nice %atom)
        [4 q:$(gen p.gen, gol %atom)]
      ::
          [%dttr *]                                                   ::  .*
        :-  (nice %blur)
        [2 q:$(gen p.gen, gol %blur) q:$(gen q.gen, gol %blur)]
      ::
          [%dtsg *]                                                   ::  .~ 
        :-  (nice [%cube p.gen])
        [1 p.gen]
      ::
          [%ktbn *]                                                   ::  ^=
        =+  vat=$(gen q.gen)
        [(face p.gen p.vat) q.vat]
      ::
          [%ktld *]                                                   ::  ^>
        =+  hum=$(gen q.gen)
        :-  p.hum
        :+  10
          ?-    p.gen
              @   p.gen
              ^   [p.p.gen q:$(gen q.p.gen, gol %blur)]
          --
        q.hum
      ::
          [%ktdp *]                                                   ::  ^-
        =+  hif=(nice (play p.gen))
        [hif q:$(gen q.gen, gol hif)]
      ::
          [%ktpd *]                                                   ::  ^+
        =+  zut=(play p.gen)
        ?.  ?|(!vet (nest(sut gol) zut))
          ^>  bean.[%need |=((~(fly to dump(sut gol)) 75))]
          ^>  bean.[%have |=((~(fly to dump(sut zut)) 75))]
          ^>(bean.'type-fail' !!)
        $(gen q.gen, gol zut)
      ::
          [%ktsg *]                                                   ::  ^~
        =+  cag=burn  
        =+  nef=$(gen p.gen, sut p.cag)
        [p.nef [1 ^>(bean.%ketsig-nock .*(q.cag q.nef))]]
      ::
          [%mtbn *]                                                   ::  %=
        =+  lar=(seek p.gen)
        =+  fup=(emit p.lar r.lar q.gen)
        ?~  q.lar
          [(nice p.fup) q.fup]
        :-  (nice (ripe(sut p.fup) q.u.q.lar))
        [9 p.u.q.lar q.fup]
      ::
          [%zpbn *]                                                   ::  !=
        :-  (nice %blur)
        [1 q:$(gen p.gen)]
      ::
          [%zpcb *]                                                   ::  !_
        :: ^>  bean.[%spot |=((~(fly to ~(loc go p.gen)) 75))]
        $(gen q.gen)
      ::
          [%zpdx *]                                                   ::  !#
        ^>  ping
        $(gen p.gen)
      ::
          [%zpzp ~]                                                   ::  !!
        :-  %blot
        [0 0]
      ::
          *           
        =+(doz=(open gen) ?:(.=(doz gen) ^>(bean.%open !!) $(gen doz)))
      --
    ::
    ::::
    ==
  ::::
  ::
      nest
    ^<  mine.[!=(->) %nest ~]
    =+  ref=*type
    |=
    ^>  bean.%nest
    ^-  ?
    =+  gil=*(pool &[p=*type q=*type])
    =<  dext
    |*
    ::::
    ::
        dext
      ^-  ?
      ?:  .=(sut ref)
        &
      ?-  sut
          %atom       ?-(ref %atom &, [%cube *] !.?(p.ref), * sint)
          %blot       sint
          %blur       &
      ::
          [%cell *]
        ?.  ?=([%cell *] ref)
          sint
        ?&
          dext(sut p.sut, ref p.ref)
          dext(sut q.sut, ref q.ref)
        --
      ::
          [%core *]   
        ?.  ?=([%core *] ref) 
          sint 
        ?&
          dext(sut p.sut, ref p.ref)
          =>(.(sut dirt, ref dirt(sut ref)) .=(+>.sut +>.ref))
        --
      ::
          [%cube *]   ?.(?=([%cube *] ref) sint .=(sut ref))
          [%face *]   dext(sut q.sut)
          [%fork *]
        ?.  ?=(*<%atom %blur [%cell *] [%cube *] [%core *]> ref)
          sint
        ?|(dext(sut p.sut) dext(sut q.sut))
      ::
          [%hold *]
        ?|
          (~(has in gil) [sut ref])
          dext(gil (~(put in gil) [sut ref]), sut repo)
        --
      --
    ::::
    ::
        sint
      ^-  ?
      ?-  ref
          %atom       |
          %blot       &
          %blur       |
          [%cell *]   |
          [%core *]   dext(ref [%cell p.ref %blur])
          [%cube *]   ?&
                        .?(p.ref) 
                        dext(ref [%cell [%cube -.p.ref] [%cube +.p.ref]])
                      --
          [%face *]   dext(ref q.ref)
          [%fork *]   ?&(dext(ref p.ref) dext(ref q.ref))
          [%hold *]
        ?|
          (~(has in gil) [sut ref])
          dext(gil (~(put in gil) [sut ref]), ref repo(sut ref))
        --
      --
    ::
    ::::
    ==
  ::::
  ::
      orth
    ^<  mine.[!=(->) %orth ~]
    =+  ref=*type
    |=
    ^>  bean.%orth
    =+  bix=*(pool &[*type *type])
    |-
    ?-    sut
        %atom       ?-(ref %atom |, [%cell *] &, * $(sut ref, ref sut))
        %blot       &
        %blur       (nest(sut %blot) ref)
        [%cell *]
      ?:  ?=([%cell *] ref)
        ?|($(sut p.sut, ref p.ref) $(sut q.sut, ref q.ref))
      $(sut ref, ref sut)
    ::
        [%core *]   $(sut [%cell p.sut %blur])
        [%cube *]
      ?-  ref
          %atom       .?(p.sut)
          [%cell *]   ?|  !.?(p.sut)
                          $(sut [%cell [%cube -.p.sut ] [%cube +.p.sut]])
                      --
          [%cube *]   !.=(p.sut p.ref)
          *           $(sut ref, ref sut)
      --
    ::
        [%face *]   $(sut q.sut)
        [%fork *]   ?&($(sut p.sut) $(sut q.sut))
        [%hold *]
      ?|
        (~(has in bix) [ref sut])
        $(bix (~(put in bix) [ref sut]), sut repo)
      --
    --
  ::::
  ::
      peek
    ^<  mine.[!=(->) %peek ~]
    =+  axe=*axis
    |=
    ^-  *type
    ?:  .=(1 axe)
      sut
    =+  [tip=(cap axe) tap=(mas axe)]
    ?-    sut
        %atom       %blot
        %blot       %blot
        %blur       %blur
        [%cell *]   ?:(.=(2 tip) $(sut p.sut, axe tap) $(sut q.sut, axe tap))
        [%core *]   ?:(.=(2 tip) $(sut p.sut, axe tap) %blur)
    ::
        [%cube *]   
      ?.  .?(p.sut) 
        %blot 
      ?:  .=(2 tip) 
        $(sut [%cube -.p.sut], axe tap)
      $(sut [%cube +.p.sut], axe tap)
    ::
        [%face *]   $(sut q.sut)
        [%fork *]   (fork $(sut p.sut) $(sut q.sut))
        [%hold *]   $(sut repo)
    --
  ::::
  ::
      play
    =+  gen=*gene
    |=
    :: ^>  bean.[%mo-play |=((~(fly to ~(tem go ?-(gen @ gen, ^ -.gen))) 75))]
    =>  .(vet |)
    ^-  *type
    ?-    gen
        [%bnld *]                                                     ::  =>
      $(gen q.gen, sut $(gen p.gen))
    ::
        [%brmt *]                                                     ::  |%
      [%core sut %pink [| (~(gas by *(book term gene)) p.gen)]]
    ::
        [%brtr *]                                                     ::  |*
      [%core sut %blue [| (~(gas by *(book term gene)) p.gen)]]
    ::
        [%csbn *]                                                     ::  ?=
      flag
    ::
        [%csdg *]                                                     ::  ?:
      =+  fex=(gain p.gen)
      ?:  .=(%blot fex)
        $(gen r.gen)
      (fork $(sut fex, gen q.gen) $(gen r.gen))
    ::
        [%dgdp *]                                                     ::  :-
      (cell $(gen p.gen) $(gen q.gen))
    ::
        [%dtbn *]                                                     ::  .=
      flag
    ::
        [%dtcs *]                                                     ::  .?
      flag
    ::
        [%dtkt *]                                                     ::  .^
      %atom
    ::
        [%dttr *]                                                     ::  .*
      %blur
    ::
        [%dtsg *]                                                     ::  .~ 
      [%cube p.gen]
    ::
        [%ktbn *]                                                     ::  ^=
      [%face p.gen $(gen q.gen)]
    ::
        [%ktld *]                                                     ::  ^>
      $(gen q.gen)
    ::
        [%ktdp *]                                                     ::  ^-
      $(gen p.gen)
    ::
        [%ktpd *]                                                     ::  ^+
      $(gen q.gen)
    ::
        [%ktsg *]                                                     ::  ^~
      $(gen p.gen)
    ::
        [%mtbn *]                                                     ::  %=
      =+  lar=(seek p.gen)
      =+  wix=(edit r.lar q.gen)
      ?~  q.lar
        wix
      (ripe(sut wix) q.u.q.lar)
    ::
        [%zpbn *]                                                     ::  !=
      %blur
    ::
        [%zpdx *]                                                     ::  !#
::      ^>  ping
      $(gen p.gen)
    ::
        [%zpcb *]                                                     ::  !_
      $(gen q.gen)
    ::
        [%zpzp ~]                                                     ::  !!
      %blot
    ::
        *           
      =+(doz=(open gen) ?:(.=(doz gen) ^>(bean.%open !!) $(gen doz)))
    --
  ::::
  ::
      read  ^<(mine.[!=(->) %read ~] =+(txt=*tape |=(^-(*gene [%read !!]))))
  ::::
  ::
      reco
    =+  fud==+(p=*type |=(p))
    |:
    =+  por=repo
    =+  yot=(fud por)
    ?:  .=(yot por)
      sut
    yot
  ::::
  ::
      repo
    ^>  bean.%repo
    ^-  *type
    ?-    sut
        %blur       [%cell %blur %blur]
        [%cube *]   ?.(.?(p.sut) sut [%cell [%cube -.p.sut] [%cube +.p.sut]])
        [%core *]   [%cell p.sut %blur]
        [%face *]   q.sut
        [%hold *]   
      ^>  memo
      ?:  (~(has in fan) sut)
        ^>(bean.'inference recursion' !!)
      =>  .(fan (~(put in fan) sut))
      =+  gar=(turn q.sut =+(p=*gene |=((play(sut p.sut) p))))
      =+  gun=(~(gas in *(pool type)) gar)    ::  XX
      =+  nog=(~(tap in gun) *(list type))
      (roll nog =+([p=*type q=`*type`%blot] |=((fork p q))))
    ::
        *   sut
    --
  ::::
  ::
      ripe
    ^<  mine.[!=(->) %ripe ~]
    =+  hag=*(list gene)
    |=
    ^>  bean.%ripe
::  =-  [%hold typ ~(tap in (~(gas in *(pool gene)) hag))]
    =-  [%hold typ hag]   ::  XX
    ^=  typ
    ?>  ?=([%core *] sut)
    ?-    q.sut
        %blue       sut
        [%gray *]   
      ?.  ?|  !vet
              (nest(sut p.q.sut) p.sut)
          -- 
        ^>  bean.[%need |=((~(fly to dump(sut p.q.sut)) 75))]
        ^>  bean.[%have |=((~(fly to dump(sut p.sut)) 75))]
        ^>(bean.'mitbon-gray' !!)
      [%core p=p.q.sut q=%blue r=r.sut]
    ::
        %pink   sut
    ::
        [%teal *]
      =+  dox=[%core p.q.sut %pink r.sut]
      ?.  ?|  !vet
              .=(sut dox)
              (levy hag =+(p=*gene |=((roam dox p))))
          --
        ^>  bean.[%need |=((~(fly to dump(sut p.q.sut)) 75))]
        ^>  bean.[%have |=((~(fly to dump(sut p.sut)) 75))]
        ^>(bean.'mitbon-teal' !!)
      sut
    --
  ::::
  ::
      roam
    ^<  mine.[!=(->) %roam ~]
    =+  [dox=*type gen=*gene]
    |=
    ?|  (~(has in rib) [sut dox gen])
        .=  q:(mill(rib (~(put in rib) [sut dox gen])) %blur gen)
          q:(mill(sut dox, vet |) %blur gen)
    --
  ::::
  ::
      seek
    ^<  mine.[!=(->) %seek ~]
    =+  hep=*rope
    |=
    ^>  bean.%seek
    ^-  *plan
    ?~  hep
      [1 ~ sut]
    =+  zar=$(hep t.hep)
    =>  %=    .           ::  cumbersome; should fork .
            r.zar 
          ?~  q.zar 
            r.zar 
          ?:  ?=([[~ *] *] t.hep)
            r.zar 
          (ripe(sut r.zar) [[~ 1] ~])
        --
    ?-    i.hep
        @
      =+  hup=(find(sut r.zar) i.hep)
      [(peg p.zar p.hup) q.hup r.hup]
    ::
        [~ *]
      [(peg p.zar p.i.hep) ~ (peek(sut r.zar) p.i.hep)]
    --
  ::::
  ::
      snap
    ^<  mine.[!=(->) %snap ~]
    =+  [ref=*type gen=*gene]
    |=
    ^>  bean.%snap
    ^-  *gene 
    ?-    sut
        [%cell *]
      =+  haq=(hack gen)
      ?-  -.haq
        &   [%dgdp $(sut p.sut, gen p.haq) $(sut q.sut, gen q.haq)]
        |   p.haq
      --
    ::
        [%face *]   [%ktbn p.sut $(sut q.sut)]
        [%hold *]   $(sut repo)
        *           gen
    --
  ::::
  ::
      tuck
    ^<  mine.[!=(->) %tuck ~]
    =+  [hep=*rope boz=*type rix=*gene]
    |=
    ^>  bean.%tuck
    =+  peh=(flop hep)
    =+  axe=`@`1
    |-
    ^-  [p=*type q=[p=*axis q=*tool]]
    ?~  peh
      =+  lof=(mill(sut boz) %blur rix)
      [p.lof [axe q.lof]]
    ?-    i.peh
        @
      =+  wer=(find i.peh)
      =+  giv=$(sut r.wer, axe (peg axe p.wer), peh t.peh)
      [(heal i.peh p.wer p.giv) q.giv]
    ::
        [~ *]
      =+  vas=(peek p.i.peh)
      =+  ^=  giv
          %=    $
              sut   vas
              rix   ?~(t.peh (snap(sut vas) boz rix) rix)
              axe   (peg axe p.i.peh)
              peh   t.peh
          --
      [(haul p.i.peh p.giv) q.giv]
    --
  ::::
  ::
      tusk
    ^<  mine.[!=(->) %tusk ~]
    =+  [hep=*rope boz=*type rix=*gene]
    |=
    ^>  bean.%tusk
    =+  peh=(flop hep)
    |-
    ^-  *type
    ?~  peh
      (play(sut boz) rix)
    ?-    i.peh
        @
      =+  wer=(find i.peh)
      (heal i.peh p.wer $(sut r.wer, peh t.peh))
    ::
        [~ *]
      =+  vas=(peek p.i.peh)
      %-  haul
      :-  p.i.peh
      $(sut vas, peh t.peh, rix ?~(t.peh (snap(sut vas) boz rix) rix))
    --
  ::
  ::::
  ==
==
